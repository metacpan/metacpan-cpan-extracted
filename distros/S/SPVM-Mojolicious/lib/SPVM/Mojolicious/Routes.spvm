# Copyright (c) 2025 Yuki Kimoto
# MIT License

class Mojolicious::Routes extends Mojolicious::Routes::Route {
  version_from Mojolicious;
  
  use Hash;
  use Mojolicious::Callback;
  use Mojolicious::Controller;
  use Mojolicious::Routes::Match::StackData;
  
  # Class Methods
  static method new : Mojolicious::Routes () {
    
    my $self = new Mojolicious::Routes;
    
    $self->init;
    
    return $self;
  }
  
  # Instance Methods
  protected method init : void ($options : object[] = undef) {
    
    $self->SUPER::init;
  }
  
  method dispatch : int ($c : Mojolicious::Controller) {
    
    $self->match($c);
    
    unless (@{$c->match->stack}) {
      return 0;
    }
    
    $self->continue($c);
    
    return 1;
  }
  
  private method match : void ($c : Mojolicious::Controller) {
    
    my $req  = $c->req;
    my $path = $req->url->path->to_route;
    
    # Method (HEAD will be treated as GET)
    my $method = Fn->uc($req->method);
    if ($method eq "HEAD") {
      $method = "GET";
    }
    
    my $ws = $c->tx->is_websocket ? 1 : 0;
    my $match = $c->match;
    $match->set_root($self);
    
    my $options = Hash->new({method => $method, path => $path, websocket => $ws});
    
    # Check routes
    $match->find($options);
    
    my $endpoint = $match->endpoint;
    
    unless ($endpoint) {
      return;
    }
  }
  
  private method continue : int ($c : Mojolicious::Controller) {
    
    my $match = $c->match;
    my $stack = $match->stack;
    my $position = $match->position;
    
    unless ($position < @$stack) {
      die "[Unexpected Error]Invalid stack position.";
    }
    
    my $stack_data = $stack->[$position];
    my $captures = $stack_data->captures;
    
    $c->set_mojo_captures($captures);
    
    my $r = $stack_data->route;
    
    my $continue = 1;
    if (my $cb = $r->cb) {
      $c->log->trace("Routing to a callback");
      
      $continue = $cb->($c);
    }
    elsif (my $template_name = $r->pattern->defaults->{"template"}->(string)) {
      
      $c->log->trace("Routing to the template $template_name");
      
      $c->render($template_name, $r->pattern->defaults->to_options);
      
      $continue = 0;
    }
    
    my $last = ($position == @$stack - 1);
    if ($last) {
      $continue = 0;
    }
    
    $match->set_position($position + 1);
    
    if ($continue) {
      return $self->continue($c);
    }
  }
  
}
