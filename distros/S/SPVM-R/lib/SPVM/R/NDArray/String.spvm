# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::String extends R::NDArray {
  
  use Fn;
  use Array;
  use R::NDArray::StringBuffer;
  use R::NDArray::Time::Piece;
  use R::OP::StringBuffer;
  use R::OP::Time::Piece;
  
  # Fields
  method data : string[] () {
    
    return (string[])$self->{data};
  }
  
  # Class Mehtods
  static method new : R::NDArray::String ($options : object[] = undef) {
    
    my $self = new R::NDArray::String;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method create_default_data : string[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new string[$length];
    
    return $data;
  }
  
  method elem_to_string : string ($data : string[], $data_index : int) {
    
    my $elem = $data->[$data_index];
    
    unless ($elem) {
      return "undef";
    }
    
    my $string = copy $elem;
    
    return $string;
  }
  
  method elem_assign : void ($dist_data : string[], $dist_data_index : int, $src_data : string[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_clone : void ($dist_data : string[], $dist_data_index : int, $src_data : string[], $src_data_index : int) {
    
    my $src_elem = $src_data->[$src_data_index];
    
    if ($src_elem) {
      $dist_data->[$dist_data_index] = copy $src_elem;
    }
    else {
      $dist_data->[$dist_data_index] = undef;
    }
  }
  
  method elem_cmp : int ($data : string[], $a_data_index : int, $b_data_index : int) {
    
    my $cmp = $data->[$a_data_index] cmp $data->[$b_data_index];
    
    return $cmp;
  }
  
  method clone : R::NDArray::String ($shallow : int = 0) {
    return (R::NDArray::String)$self->SUPER::clone($shallow);
  }
  
  method slice : R::NDArray::String ($asix_indexes_product : R::NDArray::Int[]) {
    return (R::NDArray::String)$self->SUPER::slice($asix_indexes_product);
  }
  
  precompile method to_int_ndarray : R::NDArray::Int () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Int->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = Fn->to_int($data->[$i]);
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_long_ndarray : R::NDArray::Long () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Long->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = Fn->to_long($data->[$i]);
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_float_ndarray : R::NDArray::Float () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Float->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] =Fn->to_float($data->[$i]);
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_double_ndarray : R::NDArray::Double () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Double->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = Fn->to_double($data->[$i]);
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_string_buffer_ndarray : R::NDArray::StringBuffer () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::StringBuffer->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      my $elem = $data->[$i];
      
      if ($elem) {
        $ret_data->[$i] = StringBuffer->new($data->[$i]);
      }
      else {
        $ret_data->[$i] = undef;
      }
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_time_piece_ndarray : R::NDArray::Time::Piece () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Time::Piece->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      my $elem = $data->[$i];
      
      if ($elem) {
        if (length $elem == 10) {
          
          my $tp = (Time::Piece)undef;
          
          eval { $tp = Time::Piece->strptime($data->[$i], "%Y-%m-%d"); }
          
          $ret_data->[$i] = $tp;
        }
        elsif (length $elem == 19) {
          
          my $tp = (Time::Piece)undef;
          
          eval { $tp = Time::Piece->strptime($data->[$i], "%Y-%m-%d %H:%M:%S"); }
          
          $ret_data->[$i] = $tp;
        }
        else {
          die "The string \"$elem\" is invalid date or datetime format.";
        }
      }
      else {
        $ret_data->[$i] = undef;
      }
    }
    
    return $ret_ndarray;
  }
  
}
