# -*- cperl -*-
# ABSTRACT: TemplateStore

=head1 SYNOPSIS

This class delivers a store object from which you can retrieve templates
(HTML and TeX). The store resides in the share/templates directory of your
BeamerReveal package installation.
Underneath, there is only one store, but multiple objects can access it.

=cut

package BeamerReveal::TemplateStore;

use parent 'Exporter';
use Carp;

my $store = undef;

=method new()

  $store = BeamerReveal::TemplateStore->new()

creates a store object C<$store> to query for templates.

=cut

sub new {
  my $class = shift;
  
  $class = (ref $class ? ref $class : $class );
  my $self = {};
  bless $self, $class;
}


=method fetch()

  $t = fetch( $library, $fileName )

This method will fetch a template from the store. Templates are cached in the store, so they only need to be read from disk once.

=over 4

=item . C<$library>

'html' or 'tex'. This will determine in which subfolder  of the share/templates/ your template will be
searched  for.

=item . C<$fileName>

name of the template to fetch

=item . C<$t>

template that has been fetched from the store

=back

=cut

sub fetch {
  my $self = shift;
  my ( $library, $fileName ) = @_;

  my $key = $library . '/' . $fileName; 
  $store->{$key} = _readTemplate( $library, $fileName ) unless( exists $store->{$key} );
  return $store->{$key};
}

=method stampTemplate()

  $s = stampTemplate( $string, $hash )

This method will replace the C<---KEY---> stamps in C<$string> with VALUE, based on the (KEY,VALUE) pairs of the C<%$hash>.

=over 4

=item . C<$string>

string to replace the stamps in.

=item . C<$hash>

reference to a hash containing the (KEY,VALUE) pairs relating a stamp to its actual value.

=item . C<$s>

string in which the replacements took place

=back

=cut

sub stampTemplate {
  my ( $string, $hash ) = @_;
  
  while( my ( $stamp, $value ) = each %$hash ) {
    if ( defined( $value ) ) {
      $string =~ s/---${stamp}---/$value/g;
    }
    else {
      my $lcstamp = lc( $stamp );
      $string =~ s/(?:${lcstamp}\s*[:=]\s*)?"?-*-${stamp}-*-"?//g;
    }
  }
  return $string;
}

=method $t = _readTemplate( $library, $fileName )

This method will actually read a template from the disk. Don't use this method directly. Always use the C<fetch()> function.

=over 4

=item . C<$library>

'html' or 'tex'. This will determine in which subfolder of the share/templates/ your template will be
searched for.

=item . C<$fileName>

name of the template to read

=item . C<$t>

template that has been read from disk

=back

=cut

sub _readTemplate {
  my ( $library, $fileName ) = @_;

  say STDERR "    - reading $fileName template";
  
  my $home = $^O eq 'MSWin32' ? $ENV{'userprofile'} : $ENV{'HOME'};
  my $configDir = $ENV{'BEAMERREVEAL_CONFIG'}
    // File::Spec->catfile( $home, '.config', 'beamer-reveal' );
  my $templateFileName = File::Spec->catfile( $configDir,
					      'templates', $library,
					      $fileName );
  $templateFileName = File::Spec->catfile( File::ShareDir::dist_dir( 'beamer-reveal' ),
					   'templates', $library,
					   $fileName )
    if ( ! -r $templateFileName );
  
  my $templateFile = IO::File->new();
  $templateFile->open( "<$templateFileName" )
    or die( "Error: installation incomplete - cannot find the template file '$fileName'\n" );
  my $content = do { local $/; <$templateFile> };
  return $content;
}


1;
