# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::String extends R::NDArray::Object {
  
  use Fn;
  use Array;
  
  # Class Mehtods
  static method new : R::NDArray::String ($options : object[] = undef) {
    
    my $self = new R::NDArray::String;
    
    $self->init($options);
    
    return $self;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    my $element_size = Fn->sizeof_native_pointer;
    
    $options = Fn->merge_options($options, {element_size => $element_size, type => "string"});
    
    $self->SUPER::init($options);
  }
  
  method data : string[] () {
    
    return (string[])$self->{data};
  }
  
  protected method check : void () {
    
    my $data = (string[])$self->{data};
    
    my $length = $self->length;
    
    unless (@$data == $length) {
      die "The length of data field must be the same as length field.";
    }
  }
  
  protected method create_default_data : string[] ($length : int) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new string[$length];
    
    return $data;
  }
  
  method clone : R::NDArray::String () {
    return (R::NDArray::String)$self->SUPER::clone;
  }
  
  method element_to_string : string ($data_index : int) {
    
    my $data = $self->data;
    
    my $element = $data->[$data_index];
    
    my $string = $element;
    
    return $string;
  }
  
  method element_clone : string ($data_index : int) {
    
    my $data = $self->data;
    
    my $element = $data->[$data_index];
    
    my $clone = copy $element;
    
    return $clone;
  }
  
  precompile method to_int : R::NDArray::Int () {
    
    unless ($self) {
      die "The n-dimension array \$self must be defined.";
    }
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Int->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (int)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_long : R::NDArray::Long () {
    
    unless ($self) {
      die "The n-dimension array \$self must be defined.";
    }
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Long->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (long)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_float : R::NDArray::Float () {
    
    unless ($self) {
      die "The n-dimension array \$self must be defined.";
    }
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Float->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (float)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_double : R::NDArray::Double () {
    
    unless ($self) {
      die "The n-dimension array \$self must be defined.";
    }
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Double->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (double)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_float_complex : R::NDArray::FloatComplex () {
    die "This method will be implemented.";
  }
  
  precompile method to_double_complex : R::NDArray::DoubleComplex () {
    die "This method will be implemented.";
  }
  
  precompile method to_string_buffer : R::NDArray::StringBuffer () {
    
    unless ($self) {
      die "The n-dimension array \$self must be defined.";
    }
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::StringBuffer->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = StringBuffer->new($data->[$i]);
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_time_piece : R::NDArray::Time::Piece () {
    
    unless ($self) {
      die "The n-dimension array \$self must be defined.";
    }
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Time::Piece->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      if ($data->[$i]) {
        if (length $data->[$i] == 10) {
          
          my $tp = (Time::Piece)undef;
          
          eval { $tp = Time::Piece->strptime($data->[$i], "%Y-%m-%d"); }
          
          $ret_data->[$i] = $tp;
        }
        elsif (length $data->[$i] == 19) {
          
          my $tp = (Time::Piece)undef;
          
          eval { $tp = Time::Piece->strptime($data->[$i], "%Y-%m-%d %H:%M:%S"); }
          
          $ret_data->[$i] = $tp;
        }
        else {
          $ret_data->[$i] = undef;
        }
      }
      else {
        $ret_data->[$i] = undef;
      }
    }
    
    return $ret_ndarray;
  }
  
}
