class File::Spec::Instance::Unix extends File::Spec::Instance {
  use Regex;
  use StringList;
  use Cwd;
  use Sys;
  use Sys::FileTest;
  
  # Class Methods
  static method new : File::Spec::Instance::Unix () {
    
    my $self = new File::Spec::Instance::Unix;
    
    return $self;
  }
  
  # Instance Methods
  method devnull : string () {
    return "/dev/null";
  }
  
  method rootdir : string () {
    return "/";
  }
  
  method curdir : string () {
    return ".";
  }

  method updir : string () {
    return "..";
  }

  method canonpath : string ($path : string) {
    unless ($path) {
      die "The \$path must be defined";
    }
    
    # xx////xx  -> xx/xx
    $path = Regex->new("/{2,}")->replace_g($path, "/");
    
    # xx/././xx -> xx/xx
    $path = Regex->new("(?:/\.)+(?:/|\z)")->replace_g($path, "/");
    
    # ./xx      -> xx
    unless ($path eq "./") {
      $path = Regex->new("^(?:\./)+", "s")->replace_g($path, "");
    }
    
    # /../../xx -> xx
    $path = Regex->new("^/(?:\.\./)+")->replace($path, "/");
    
    # /..       -> /
    $path = Regex->new("^/\.\.$")->replace($path, "/");
    
    # xx/       -> xx
    unless ($path eq "/") {
      $path = Regex->new("/\z")->replace($path, "");
    }
    
    return $path;
  }

  method catdir : string ($dir_parts : string[]) {
    my $interface = (File::Spec::Instance::Interface)$self;
    
    unless ($dir_parts) {
      die "The \$dir_parts must be defined";
    }
    
    my $dir_parts_list = StringList->new($dir_parts);
    $dir_parts_list->push("");
    $dir_parts = $dir_parts_list->to_array;
    
    my $path = Fn->join("/", $dir_parts);
    
    my $canonpath = $interface->canonpath($path);
    
    return $canonpath;
  }
  
  method catfile : string ($dir_parts : string[], $file_base_name : string) {
    my $interface = (File::Spec::Instance::Interface)$self;
    
    unless ($dir_parts) {
      die "The \$dir_parts must be defined";
    }
    
    unless ($file_base_name) {
      die "The \$file_base_name must be defined";
    }
    
    $file_base_name = $interface->canonpath($file_base_name);
    
    unless (@$dir_parts) {
      return $file_base_name;
    }
    
    my $dir = $interface->catdir($dir_parts);
    my $dir_length = length $dir;
    
    unless ($dir_length >= 1 && $dir->[$dir_length - 1] == '/') {
      $dir .= "/";
    }
    
    my $file = "$dir$file_base_name";
    
    return $file;
  }
  
  method no_upwards : string[] ($dir_parts : string[]) {
    my $no_upwards_list = StringList->new;
    
    my $re = Regex->new("^\.{1,2}\z", "s");
    for my $dir_part (@$dir_parts) {
      unless ($re->match($dir_part)) {
        $no_upwards_list->push($dir_part);
      }
    }
    
    my $no_upwards = $no_upwards_list->to_array;
    
    return $no_upwards;
  }
  
  method file_name_is_absolute : int ($path : string) {
    my $re = Regex->new("^/", "s");
    my $is_abs = 0;
    if ($re->match($path)) {
      $is_abs = 1;
    }
    return $is_abs;
  }
  
  method join : string ($dir_parts : string[], $file_base_name : string) {
    my $interface = (File::Spec::Instance::Interface)$self;
    
    return $interface->catfile($dir_parts, $file_base_name);
  }
  
  method catpath : string ($volume : string, $directory : string, $file : string) {
    unless ($directory) {
      $directory = "";
    }
    unless ($file) {
      $file = "";
    }
    
    my $directory_length = length $directory;
    my $file_length = length $file;
    if ( $directory ne ""                && 
         $file ne ""                     && 
         $directory->[$directory_length - 1] != '/' && 
         $file->[0] != '/')
    {
      $directory .= "/$file" ;
    }
    else {
      $directory .= $file ;
    }
    
    return $directory;
  }

  method splitpath : string[] ($path : string, $no_file = 0 : int) {
    
    my $volume = "";
    my $directory = "";
    my $file = "";
    
    if ( $no_file ) {
      $directory = $path;
    }
    else {
      my $re = Regex->new("^((?:.*/(?:\.\.?\z)?)?)([^/]*)", "s");
      if ($re->match($path)) {
        $directory = $re->cap1;
        $file      = $re->cap2;
      }
    }
    
    return [$volume, $directory, $file];
  }

  method rel2abs : string ($path : string, $base = undef : string) {
    my $interface = (File::Spec::Instance::Interface)$self;
    
    # Clean up $path
    if (!$interface->file_name_is_absolute($path)) {
      # Figure out the effective $base and clean it up.
      if (!$base || $base eq "") {
        $base = Cwd->getcwd;
      }
      elsif (!$interface->file_name_is_absolute($base)) {
        $base = $interface->rel2abs($base) ;
      }
      else {
        $base = $interface->canonpath($base);
      }
      # Glom them together
      $path = $interface->catdir([$base, $path]);
    }
    
    my $rel2abs = $interface->canonpath($path);
    
    return $rel2abs;
  }
  
  private static method re_split : string[] ($re_separator : Regex, $string : string, $limit = -1 : int) {
    unless ($re_separator) {
      die "The \$separator must be defined";
    }
    
    unless ($string) {
      die "The \$string must be defined";
    }
    
    my $string_length = length $string;
    
    if ($limit == 0) {
      die "The \$limit can't be 0";
    }
    
    my $separated_strings_list = StringList->new_len(0);
    
    my $string_base = 0;
    my $match_count = 0;
    while (1) {
      if ($limit > 0 && $match_count >= $limit - 1) {
        last;
      }
      
      my $before_string_base = $string_base;
      my $match = $re_separator->match_forward($string, \$string_base);
      if ($match) {
        $match_count++;
        
        my $separated_string = Fn->substr($string, $before_string_base, $string_base - $before_string_base);
        $separated_strings_list->push($separated_string);
      }
      else {
        last;
      }
    }
    if ($string_base == $string_length) {
      $separated_strings_list->push("");
    }
    else {
      my $separated_string = Fn->substr($string, $string_base, $string_length - $string_base);
      $separated_strings_list->push($separated_string);
    }
    
    my $separated_strings = $separated_strings_list->to_array;
    
    return $separated_strings;
  }

  method splitdir : string[] ($path : string) {
    my $dirs = Fn->split("/", $path, -1);
    return $dirs;
  }

  method abs2rel : string ($path : string, $base = undef : string) {
    my $interface = (File::Spec::Instance::Interface)$self;
    
    unless ($base && length $base) {
      $base = Cwd->getcwd();
    }
    
    $path = $interface->canonpath($path);
    $base = $interface->canonpath($base);

    my $path_directories = (string)undef;
    my $base_directories = (string)undef;
    
    my $pash_is_abs = $interface->file_name_is_absolute($path);
    my $base_is_abs =  $interface->file_name_is_absolute($base);
    
    if ($pash_is_abs || $base_is_abs) {
      $path = $interface->rel2abs($path);
      $base = $interface->rel2abs($base);
      
      my $path_volume = $interface->splitpath($path, 1)->[0];
      my $base_volume = $interface->splitpath($base, 1)->[0];

      # Can't relativize across volumes
      unless ($path_volume eq $base_volume) {
        return $path ;
      }
      
      $path_directories = $interface->splitpath($path, 1)->[1];
      $base_directories = $interface->splitpath($base, 1)->[1];

      # For UNC paths, the user might give a volume like //foo/bar that
      # strictly speaking has no directory portion.  Treat it as if it
      # had the root directory for that volume.
      if (!length($base_directories) && $interface->file_name_is_absolute($base)) {
        $base_directories = $interface->rootdir;
      }
    }
    else {
      my $wd= $interface->splitpath(Cwd->getcwd(), 1)->[1];
      $path_directories = $interface->catdir([$wd, $path]);
      $base_directories = $interface->catdir([$wd, $base]);
    }

    # Now, remove all leading components that are the same
    my $pathchunks = $interface->splitdir( $path_directories );
    my $basechunks = $interface->splitdir( $base_directories );

    if ($base_directories eq $interface->rootdir) {
      if ($path_directories eq $interface->rootdir) {
        return $interface->curdir;
      }
      
      my $pathchunks_list = StringList->new($pathchunks);
      $pathchunks_list->shift;
      $pathchunks = $pathchunks_list->to_array;
      return $interface->canonpath( $interface->catpath("", $interface->catdir($pathchunks ), "") );
    }

    my $common_list = StringList->new;
    my $pathchunks_list = StringList->new($pathchunks);
    my $basechunks_list = StringList->new($basechunks);
    while ($pathchunks_list->length > 0 && $basechunks_list->length > 0 && $interface->_same($pathchunks_list->get(0), $basechunks_list->get(0))) {
      $common_list->push($pathchunks_list->shift);
      $basechunks_list->shift;
    }
    
    unless ($pathchunks_list->length > 0 || $basechunks_list->length > 0) {
      return $interface->curdir;
    }
    
    # @basechunks now contains the directories the resulting relative path 
    # must ascend out of before it can descend to $path_directory.  If there
    # are updir components, we must descend into the corresponding directories
    # (this only works if they are no symlinks).
    my $reverse_base_list = StringList->new;
    while ($basechunks_list->length > 0) {
      my $dir = $basechunks_list->shift;
      if( $dir ne $interface->updir ) {
        $reverse_base_list->unshift($interface->updir);
        $common_list->push($dir);
      }
      elsif($common_list->length > 0) {
        if( $reverse_base_list->length > 0 && $reverse_base_list->get(0) eq $interface->updir ) {
          $reverse_base_list->shift;
          $common_list->pop;
        }
        else {
          $reverse_base_list->unshift($common_list->pop);
        }
      }
    }
    
    my $reverse_base = $reverse_base_list->to_array;
    for my $reserve_base_part (@$reverse_base) {
      $pathchunks_list->unshift($reserve_base_part);
    }
    $pathchunks = $pathchunks_list->to_array;
    my $result_dirs = $interface->catdir($pathchunks);
    
    return $interface->canonpath( $interface->catpath("", $result_dirs, "") );
    
    return undef;
  }
  
  method _same : int ($path1 : string, $path2 : string) {
    my $same = 0;
    if ($path1 eq $path2) {
      $same = 1;
    }
    return $same;
  }

  static method compatible_split : string[] ($separator : string, $string : string, $limit = 0 : int) {
    unless ($separator) {
      die "The \$separator must be defined";
    }
    
    unless ($string) {
      die "The \$string must be defined";
    }
    
    my $string_length = length $string;
    my $separator_length = length $separator;
    
    unless ($separator_length > 0) {
      die "The length of the \$separator must be greater than 0";
    }
    
    my $separated_strings_list = StringList->new_len(0);
    
    my $match_start = 0;
    my $string_base = 0;
    my $match_count = 0;
    for (my $i = 0; $i < $string_length; $i++) {
      if ($limit > 0 && $match_count >= $limit - 1) {
        last;
      }
      
      if ($string->[$i] == $separator->[0]) {
        $match_start = 1;
      }
      
      if ($match_start) {
        my $match = 1;
        for (my $separator_index = 0; $separator_index < $separator_length; $separator_index++) {
          if ($i + $separator_index > $string_length - 1) {
            $match = 0;
            last;
          }
          else {
            unless ($string->[$i + $separator_index] == $separator->[$separator_index]) {
              $match = 0;
              last;
            }
          }
        }
        if ($match) {
          $match_count++;
          
          my $separated_string = Fn->substr($string, $string_base, $i - $string_base);
          $separated_strings_list->push($separated_string);
          $string_base = $i + $separator_length;
          $i += $separator_length - 1;
        }
      }
    }
    if ($string_base == $string_length) {
      $separated_strings_list->push("");
    }
    else {
      my $separated_string = Fn->substr($string, $string_base, $string_length - $string_base);
      $separated_strings_list->push($separated_string);
    }
    
    if ($limit == 0) {
      while ($separated_strings_list->length > 0) {
        if ($separated_strings_list->get($separated_strings_list->length - 1) eq "") {
          $separated_strings_list->pop;
        }
        else {
          last;
        }
      }
    }
    
    my $separated_strings = $separated_strings_list->to_array;
    
    return $separated_strings;
  }

  method path : string[] () {
    
    my $env_path = Sys->getenv("PATH");
    unless ($env_path) {
      return new string[0];
    }
    
    my $pathes = &compatible_split(":", $env_path);
    
    for (my $i = 0; $i < @$pathes; $i++) {
      if ($pathes->[$i] eq "") {
        $pathes->[$i] = ".";
      }
    }
    
    return $pathes;
  }

  method tmpdir : string () {
    my $interface = (File::Spec::Instance::Interface)$self;
    
    my $env_tmpdir = Sys->getenv("TMPDIR");
    my $dirlist = [$env_tmpdir, "/tmp"];
    my $tmpdir = $interface->_tmpdir($dirlist);
    return $tmpdir;
  }

  method _tmpdir : string ($dirlist : string[]) {
    my $interface = (File::Spec::Instance::Interface)$self;
    
    my $tmpdir = (string)undef;
    for my $dir (@$dirlist) {
      if ($dir && Sys::FileTest->d($dir) && Sys::FileTest->w($dir)){
        $tmpdir = $dir;
        last;
      }
    }
    
    unless ($tmpdir) {
      $tmpdir = $interface->curdir;
    }
    
    $tmpdir = $interface->canonpath($tmpdir);
    
    if (!$interface->file_name_is_absolute($tmpdir)) {
      $tmpdir =  $interface->rel2abs($tmpdir);
      if (!$interface->file_name_is_absolute($tmpdir)) {
        die "A temporary directory must be converted to an absolute path";
      }
    }
    
    return $tmpdir;
  }
}
