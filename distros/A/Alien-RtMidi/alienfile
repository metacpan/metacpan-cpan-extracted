use alienfile;

my $RTMIDI_MIN_VERSION = '4.0.0';
my $RTMIDI_MAJOR_VERSION = 6;
my $WIN = $^O eq 'MSWin32';
my $CYG = $^O eq 'cygwin';

plugin 'Gather::IsolateDynamic';
plugin 'PkgConfig' => (
    pkg_name => 'rtmidi',
    atleast_version => $RTMIDI_MIN_VERSION
);

share {
    start_url 'https://www.music.mcgill.ca/~gary/rtmidi/release/';
    plugin 'Download' => (
        filter => qr/^rtmidi-${RTMIDI_MAJOR_VERSION}.*\.tar\.gz$/,
        version => qr/([0-9\.]+)/,
    );
    plugin 'Extract' => 'tar.gz';

    my @config = ( '--enable-static' );
    push @config, 'LDFLAGS="-std=gnu++11" CXXFLAGS="-std=gnu++11" --with-winmm' if $CYG;

    patch sub {
        my $build = shift;
        return unless $build->runtime_prop->{version} =~ /^6\.0/;
        my $patch = $build->install_prop->{patch} . '/0001-Add-a-method-to-reset-the-ok-flag-and-clear-the-erro.patch';
        my $binary = ( $WIN || $CYG ) ? '--binary' : '';
        system( "patch $binary -p1 < $patch" );
    };

    plugin $WIN
        ? 'Build::CMake'
        : 'Build::Autoconf';

    build $WIN
        ? [ [ '%{cmake3}', @{ meta->prop->{plugin_build_cmake}->{args} }, '-S', '.', '-B', 'build', '-DRTMIDI_BUILD_TESTING=0' ], '%{cmake3} --build build', '%{cmake3} --install build' ]
        : [ '%{configure} ' . join( ' ', @config ), '%{make} librtmidi.la', '%{make} install-exec-am install-data-am' ];
        # ^ This smells pretty bad, patches welcome
};
