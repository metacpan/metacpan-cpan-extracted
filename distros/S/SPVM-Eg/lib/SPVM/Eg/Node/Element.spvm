# Copyright (c) 2024 Yuki Kimoto
# MIT License

# Element in JavaScript
class Eg::Node::Element extends Eg::Node {
  allow Eg::Node;
  allow Eg::Node::Document;
  allow Eg::Node::Document::XML;
  allow Eg::Node::Document;
  
  use Fn;
  
  use Eg::Node::Element::HTML;
  
  use Eg::CSS::StyleDeclaration;
  
  has namespace_uri : ro protected string;
  
  has is_void_element : protected byte;
  
  has attributes_h : protected Hash of string;
  
  # Class Methods
  private static method new : Eg::Node::Element ($options : object[] = undef) {
    
    my $self = new Eg::Node::Element;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  protected method init : void ($options : object[] = undef) {
    
    unless ($options) {
      $options = new object[0];
    }
    
    $options = Array->merge_object($options, {node_type => Eg::Node->TYPE_ELEMENT_NODE});
    
    $self->SUPER::init($options);
  }
  
  method get_attribute : string ($name : string) {
    
    unless ($name) {
      die "\$name must be defined.";
    }
    
    if ($self isa Eg::Node::Element::HTML) {
      $name = Fn->lc($name);
      
      if ($name eq "style" && $self->has_attribute($name)) {
        return $self->style->css_text;
      }
    }
    
    my $attributes_h = $self->{attributes_h};
    
    unless ($self->{attributes_h}) {
      return undef;
    }
    
    my $attribute = $attributes_h->get_string($name);
    
    return $attribute;
  }
  
  method set_attribute : void ($name : string, $value : string) {
    
    unless ($name) {
      die "\$name must be defined.";
    }
    
    if ($self isa Eg::Node::Element::HTML) {
      $name = Fn->lc($name);
      
      if ($name eq "style") {
        
        unless ($value) {
          die "\$value must be defined.";
        }
        
        $self->style->set_css_text($value);
      }
    }
    
    unless ($value) {
      die "\$value must be defined.";
    }
    
    my $attributes_h = $self->{attributes_h};
    
    unless ($attributes_h) {
      $attributes_h = $self->{attributes_h} = Hash->new;
    }
    
    $attributes_h->set_string($name, $value);
  }
  
  method remove_attribute : void ($name : string) {
    
    unless ($name) {
      die "\$name must be defined.";
    }
    
    my $attributes_h = $self->{attributes_h};
    
    my $deleted_attribute = (string)undef;
    
    if ($self->{attributes_h}) {
      $attributes_h->delete($name);
    }
  }
  
  method has_attribute : int ($name : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    my $exists = 0;
    if ($attributes_h) {
      $exists = $attributes_h->exists($name);
    }
    
    return $exists;
  }
  
  method get_attribute_names : string[] () {
    
    my $attributes_h = $self->{attributes_h};
    
    my $names = (string[])undef;
    if ($attributes_h) {
      $names = $attributes_h->keys;
      
      Sort->sort_string_asc($names);
    }
    else {
      $names = new string[0];
    }
    
    return $names;
  }
  
  method text : string () {
    my $child_nodes = $self->child_nodes;
    
    my $child_nodes_length = @$child_nodes;
    
    my $text = (string)undef;
    
    if ($child_nodes_length > 0) {
      my $first_child = (Eg::Node)$child_nodes->[0];
      
      if ($first_child->node_type == Eg::Node->TYPE_TEXT_NODE) {
        $text = $first_child->(Eg::Node::Text)->node_value_buffer->to_string;
      }
      else {
        die "The first child is not a text node.";
      }
    }
    else {
      return "";
    }
    
    return $text;
  }
  
  method set_text : string ($text : string) {
    my $child_nodes = $self->child_nodes;
    
    my $child_nodes_length = @$child_nodes;
    
    if ($child_nodes_length > 0) {
      my $first_child = (Eg::Node)$child_nodes->[0];
      
      if ($first_child->node_type == Eg::Node->TYPE_TEXT_NODE) {
        $first_child->(Eg::Node::Text)->node_value_buffer->set($text);
      }
      else {
        die "The first child is not a text node.";
      }
    }
    else {
      my $text_node = Eg->document->implementation->create_html_document->create_text_node($text);
      
      $self->{first_child} = $text_node;
      
      $self->{last_child } = $text_node;
    }
    
  }
  
  method tag_name : string () {
    
    my $tag_name = $self->{node_name};
    
    if ($self isa Eg::Node::Element::HTML) {
      $tag_name = Fn->uc($tag_name);
    }
    
    return $tag_name;
  }
  
  method style : Eg::CSS::StyleDeclaration () { die "Not implemented."; }
  

}
