use alienfile;
use File::Which qw( which );
use Capture::Tiny qw( capture );
use List::Util qw( all );
use Sort::Versions qw( versioncmp );

configure {
  requires 'File::Which';
  requires 'Capture::Tiny';
  requires 'Sort::Versions';
};

probe sub {
  my($build) = @_;

  my %tools;
  
  foreach my $tool (qw( libtool libtoolize ))
  {
    $tools{$tool} = 0;
    my $exe = which $tool;
    if($exe)
    {
      my(undef, undef, $stdout) = capture {
        `$tool --version`;
      };
      
      if($stdout =~ /GNU libtool/ && $stdout =~ /([0-9\.]+)$/)
      {
        my $version = $1;
        require Sort::Versions;
        if(versioncmp($version, '2.4.2') >= 0)
        {
          $tools{$tool} = $version;
        }
      }
    }
  }

  if(all { $_ != 0 } values %tools)
  {
    my($version) = values %tools;
    if(all { $_ == $version } values %tools)
    {
      $build->install_prop->{my_version} = $version;
      return 'system';
    }
  }

  'share';
};

share {
  requires 'Alien::m4' => '0.11';
  requires 'Path::Tiny' => 0;

  if($^O eq 'MSWin32')
  {
    meta->prop->{env}->{PERL} = '/usr/bin/perl' if meta->prop->{platform}->{system_type} eq 'windows-mingw';
  }
  else
  {
    meta->prop->{env}->{PERL} = $^X;
  }

  if($^O eq 'openbsd')
  {
    requires 'Alien::autoconf' => '0.02';
    requires 'Alien::automake' => '0.03';
  }

  plugin Download => (
    url => 'http://ftp.gnu.org/gnu/libtool',
    filter => qr/^libtool-.*\.tar\.gz$/,
    version => qr/([0-9\.]+)/,
  );
  plugin Extract => 'tar.gz',
  plugin 'Build::Autoconf' => ( msys_version => '0.08' );
  
  patch sub {
    my($libtoolize) = Path::Tiny->new('libtoolize.in');
    $libtoolize->edit_lines(sub {
      s{['"]\@prefix\@['"]}{\`alien_libtool_root\`}g;
      s{\@datadir\@}{\$prefix/share}g;
      s{\@pkgauxdir\@}{\$prefix/share/libtool/build-aux}g;
      s{\@pkgdatadir\@}{\$prefix/share/libtool}g;
      s{\@aclocaldir\@}{\$prefix/share/aclocal}g;
    });
    my($makefile) = Path::Tiny->new('Makefile.in');
    $makefile->edit_lines(sub {
      s{\@HELP2MAN\@}{perl -e1 --}g;
    });
  };
  
  meta->after_hook(
    gather_share => sub {
      my($build) = @_;
      {
        my $exe = Path::Tiny->new('bin/alien_libtool_root');
        my $perl = $ENV{PERL} || '/usr/bin/perl';
        $exe->spew(
          "#!$perl\n",
          "use File::Spec;\n",
          "my(\$v,\$d) = File::Spec->splitpath(File::Spec->rel2abs(__FILE__));\n",
          "my \@d = File::Spec->splitdir(\$d);\n",
          "pop \@d for 1..2;\n",
          "my \$dist_dir = File::Spec->catpath(\$v,File::Spec->catdir(\@d), '');\n",
          "print \$dist_dir\n",
        );
        $exe->chmod('0700');
      }
    },
  );
};

sys {

  gather sub {
    my($build) = @_;
    $build->runtime_prop->{version} = $build->install_prop->{my_version};
  };

};
