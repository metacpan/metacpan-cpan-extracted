# Copyright (c) 2025 Yuki Kimoto
# MIT License

class Mojolicious::Plugins extends Mojo::EventEmitter {
  version_from Mojolicious;
  
  use Mojo::Loader;
  use Mojolicious;
  use Mojolicious::Plugin;
  use Native::MethodCall;
  
  # Fields
  has namespaces : rw string[];
  
  # Class Methods
  static method new : Mojolicious::Plugins () {
    
    my $self = new Mojolicious::Plugins;
    
    $self->{namespaces} = ["Mojolicious::Plugin"];
    
    return $self;
  }
  
  private static method _load : void ($class_name : string) {
    
    Mojo::Loader->load_class($class_name);
  }

  # Instance Methods
  method emit_chain : object ($name : string, $arg1 : object = undef, $arg2 : object = undef, $arg3 : object = undef) {
    
    my $wrapper = (Mojo::Callback)undef;
    
    my $subscribers = $self->subscribers($name);
    
    my $subscribers_length = @$subscribers;
    
    my $cb = (Mojo::Callback)undef;
    for (my $i = $subscribers_length - 1; $i >= 0; $i--) {
      
      $cb = $subscribers->[$i];
      
      my $next = $wrapper;
      
      $wrapper = [$cb : Mojo::Callback, $next : Mojo::Callback, $arg1 : object, $arg2 : object, $arg3 : object] method : object () {
        $cb->($next, $arg1, $arg2, $arg3);
      };
    }
    
    if (!$wrapper) {
      return undef;
    }
    else {
      return $wrapper->(undef);
    }
    
  }
  
  method emit_hook : void ($name : string, $arg1 : object = undef, $arg2 : object = undef, $arg3 : object = undef) {
    
    my $subscribers = $self->subscribers($name);
    
    my $subscribers_length = @$subscribers;
    
    for (my $i = 0; $i < $subscribers_length; $i++) {
      
      my $cb = $subscribers->[$i];
      
      $cb->($self, $arg1, $arg2, $arg3);
    }
  }
  
  method emit_hook_reverse : void ($name : string, $arg1 : object = undef, $arg2 : object = undef, $arg3 : object = undef) {
    
    my $subscribers = $self->subscribers($name);
    
    my $subscribers_length = @$subscribers;
    
    for (my $i = $subscribers_length - 1; $i >= 0; $i--) {
      
      my $cb = $subscribers->[$i];
      
      $cb->($self, $arg1, $arg2, $arg3);
    }
  }
  
  method register_plugin : void ($plugin : Mojolicious::Plugin, $app : Mojolicious, $options : object[] = undef) {
    $plugin->register($app, $options);
  }
  
}
