# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::StringBuffer extends R::NDArray::Object {
  
  use Fn;
  use Array;
  use StringBuffer;
  
  # Class Mehtods
  static method new : R::NDArray::StringBuffer ($options : object[] = undef) {
    
    my $self = new R::NDArray::StringBuffer;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  protected method init : void ($options : object[] = undef) {
    
    $self->SUPER::init($options);
  }
  
  method data : StringBuffer[] () {
    
    return (StringBuffer[])$self->{data};
  }
  
  method create_default_data : StringBuffer[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new StringBuffer[$length];
    
    return $data;
  }
  
  method elem_to_string : string ($data : StringBuffer[], $data_index : int) {
    
    my $elem = $data->[$data_index];
    
    unless ($elem) {
      return "undef";
    }
    
    my $string = $elem->to_string;
    
    return $string;
  }
  
  method elem_clone : void ($dist_data : StringBuffer[], $dist_data_index : int, $src_data : StringBuffer[], $src_data_index : int) {
    
    my $src_elem = $src_data->[$src_data_index];
    
    if ($src_elem) {
      $dist_data->[$dist_data_index] = StringBuffer->new($src_elem->to_string);
    }
    else {
      $dist_data->[$dist_data_index] = undef;
    }
  }
  
  method elem_cmp : int ($data : StringBuffer[], $a_data_index : int, $b_data_index : int) {
    
    my $cmp = $data->[$a_data_index]->to_string cmp $data->[$b_data_index]->to_string;
    
    return $cmp;
  }
  
  method clone : R::NDArray::StringBuffer ($elem_shallow_clone : int = 0) {
    return (R::NDArray::StringBuffer)$self->SUPER::clone($elem_shallow_clone);
  }
  
  method slice : R::NDArray::StringBuffer ($vector_indexes : R::NDArray::Int[]) {
    return (R::NDArray::StringBuffer)$self->SUPER::slice($vector_indexes);
  }
  
}
