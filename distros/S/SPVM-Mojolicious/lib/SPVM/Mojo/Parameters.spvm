# Copyright (c) 2025 Yuki Kimoto
# MIT License

class Mojo::Parameters {
  version_from Mojolicious;
  
  use Mojo::Util;
  use Hash;
  use Sort;
  
  # Interfaces
  interface Stringable;
  
  # Fields
  has pairs : rw string[];
  
  # Undocumented Fields
  has string : string;
  
  # Class Methods
  static method new : Mojo::Parameters ($params_value : string|object[] = undef) {
    
    my $self = new Mojo::Parameters;
    
    if ($params_value) {
      $self->parse($params_value);
    }
    
    return $self;
  }
  
  # Instance Methods
  method append : void ($pairs : object[]|Mojo::Parameters) {
    
    unless ($pairs) {
      die "The pairs \$pairs must be defined.";
    }
    
    my $pairs_array = (object[])undef;
    if ($pairs isa object[]) {
      $pairs_array = (object[])$pairs;
    }
    elsif ($pairs isa Mojo::Parameters) {
      $pairs_array = $pairs->(Mojo::Parameters)->pairs;
    }
    else {
      die "The type of \$pairs must be object[] or Mojo::Parameters.";
    }
    
    my $pairs_array_length = @$pairs_array;
    
    unless ($pairs_array_length % 2 == 0) {
      die "The length of \$pairs must be an even number if it is an array.";
    }
    
    my $pairs = StringList->new_ref($self->{pairs});
    
    my $new = $pairs_array;
    
    for (my $i = 0; $i < @$new; $i += 2) {
      my $new_name = $new->[0];
      my $new_value = $new->[1];
      
      if ($new_value) {
        if ($new_value isa string) {
          $pairs->push((string)$new_name);
          $pairs->push((string)$new_value);
        }
        elsif ($new_value isa string[]) {
          for my $one_new_value (@(string[])$new_value) {
            $pairs->push((string)$new_name);
            $pairs->push($one_new_value);
          }
        }
        else {
          die "The type of the value \$pairs->[$i] must be string or string[].";
        }
      }
    }
    
  }
  
  method clone : Mojo::Parameters () {
    
    my $clone = Mojo::Parameters->new;
    
    if ($self->{string}) {
      $clone->{string} = copy $self->{string};
    }
    else {
      $clone->{pairs} = Array->copy_string($self->{pairs});
    }
    
    return $clone;
  }
  
  method every_param : string[] ($name : string) {
    
    my $values = StringList->new;
    
    my $pairs = StringList->new_ref($self->pairs);
    
    for (my $i = 0; $i < @$pairs; $i += 2) {
      if ($pairs->[$i] eq $name) {
        $values->push($pairs->[$i + 1]);
      }
    }
    
    return $values->get_array;
  }
  
  method merge : void ($parameters : object[]|Mojo::Parameters)  {
    
    unless ($parameters) {
      die "The parameters \$parameters must be defined.";
    }
    
    my $parameters_hash = (Hash)undef;
    if ($parameters isa object[]) {
      $parameters_hash = Hash->new((object[])$parameters);
    }
    elsif ($parameters isa Mojo::Parameters) {
      $parameters_hash = $parameters->(Mojo::Parameters)->to_hash;
    }
    else {
      die "The type of the parameters \$parameters must be object[] or Mojo::Parameters.";
    }
    
    my $merge = $parameters_hash;
    my $merge_keys = $merge->keys;
    Sort->sort_string_asc($merge_keys);
    
    for my $name (@$merge_keys) {
      my $value = $merge->get($name);
      if ($value) {
        $self->set_param($name => $value);
      }
      else {
        $self->remove($name);
      }
    }
    
  }
  
  method names : string[] () {
    
    my $hash = $self->to_hash;
    my $keys = $hash->keys;
    Sort->sort_string_asc($keys);
    
    return $keys;
  }
  
  method param : string ($name : string) {
    
    my $every_param = $self->every_param($name);
    
    my $param = (string)undef;
    if (@$every_param) {
      $param = $every_param->[0];
    }
    
    return $param;
  }
  
  method set_param : void ($name : string, $value : string|string[]) {
    
    unless ($name) {
      die "The name \$name must be defined.";
    }
    
    unless ($value) {
      die "The value \$value must be defined.";
    }
    
    $self->remove($name);
    
    if ($value isa string) {
      $self->append([(string)$value]);
    }
    elsif ($value isa string[]) {
      $self->append((string[])$value);
    }
    else {
      die "The type of value \$value must be string or string[].";
    }
    
  }
  
  method parse : void ($params_value : string|object[]) {
    
    if ($params_value isa string) {
      $self->{string} = (string)$params_value;
    }
    elsif ($params_value isa object[]) {
      $self->append($params_value);
    }
    else {
      die "The type of the parameter value \$params_value must be string or object[].";
    }
  }
  
  method remove : void ($name : string) {
    
    unless ($name) {
      die "The name \$name must be defined.";
    }
    
    $self->_parse;
    
    my $pairs = StringList->new_ref($self->{pairs});
    
    my $i     = 0;
    while ($i < $pairs->length) {
      if ($pairs->get($i) eq $name) {
        $pairs->remove($i);
        $pairs->remove($i + 1);
      }
      else {
        $i += 2;
      }
    }
    
  }
  
  method to_hash : Hash () {

    my $hash = Hash->new;
    
    $self->_parse;
    
    my $pairs = StringList->new_ref($self->{pairs});
    
    for (my $i = 0; $i < $pairs->length; $i += 2) {
      my $name = $pairs->get($i);
      my $value = $pairs->get($i + 1);
      
      # Array
      if ($hash->exists($name)) {
        my $hash_value = $hash->get($name);
        
        my $hash_value_array = (string[])undef;
        if ($hash_value isa string[]) {
          $hash_value_array = (string[])$hash_value;
        }
        elsif ($hash_value isa string) {
          $hash_value_array = [(string)$hash_value];
        }
        else {
          die "[Unexpected Error]Invalid hash value type.";
        }
        
        $hash_value_array = Array->merge_string($hash_value_array, [$value]);
        
        $hash->set($name, $hash_value_array);
      }
      
      # String
      else {
        $hash->set($name, $value);
      }
    }
    
    return $hash;
  }
  
  method to_string : string () {
    
    # String (RFC 3986)
    if (my $string = $self->{string}) {
      return Mojo::Util->url_escape($string, q'^A-Za-z0-9\\-._~%!$&\'()*+,;=:@/?');
    }
    
    # Build pairs (HTML Living Standard)
    $self->_parse;
    
    my $pairs = StringList->new_ref($self->{pairs});
    
    unless ($pairs->length) {
      return "";
    }
    
    my $key_values = StringList->new;
    for (my $i = 0; $i < $pairs->length; $i += 2) {
      my $name = $pairs->get($i);
      my $value = $pairs->get($i + 1);
      
      # Escape and replace whitespace with "+"
      $name  = Mojo::Util->url_escape($name, q'^*\\-.0-9A-Z_a-z');
      $value = Mojo::Util->url_escape($value, q'^*\\-.0-9A-Z_a-z');
      
      my $name_ref = [$name];
      Re->s($name_ref, ["%20", "g"], "+");
      $name = $name_ref->[0];
      
      my $value_ref = [$value];
      Re->s($value_ref, ["%20", "g"], "+");
      $value = $value_ref->[0];
      
      $key_values->push("$name=$value");
    }
    
    return Fn->join("&", $key_values->get_array);
  }
  
  private method _parse : void () {
    
    my $string = $self->{string};
    $self->{string} = undef;
    
    $self->{pairs} = new string[0];
    
    my $pairs = StringList->new_ref($self->{pairs});
    
    if (length $string) {
      for my $pair (@{Fn->split("&", $string)}) {
        if (my $match = Re->m($pair, "^([^=]+)(?:=(.*))?$")) {
          my $name = $match->cap1;
          my $value = $match->cap2;
          
          my $name_ref = [$name];
          Re->s($name_ref, ["\+", "g"], " ");
          $name = $name_ref->[0];
          
          my $value_ref = [$value];
          Re->s($value_ref, ["\+", "g"], " ");
          $value = $value_ref->[0];
          unless ($value) {
            $value = "";
          }
          
          $name  = Mojo::Util->url_unescape($name);
          $value = Mojo::Util->url_unescape($value);
          
          $pairs->push($name);
          $pairs->push($value);
        }
      }
    }
    
  }
  
}
