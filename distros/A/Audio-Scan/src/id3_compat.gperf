%{
/*
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 *
 * $Id$
 */

#define EQ(id)    #id
#define OBSOLETE    0
#define TX(id)    #id

%}
%define hash-function-name compat_hash
id3_compat;
%%
#
# ID3v2.2 and ID3v2.3 frames
#
# Only obsolete frames or frames with an equivalent ID3v2.4 frame ID are
# listed here. If a frame ID is not listed, it is assumed that the same
# frame ID is itself the equivalent ID3v2.4 frame ID.
#
# This list may also include frames with new content interpretations; the
# translation function will rewrite the contents to comply with ID3v2.4.
#
BUF,  EQ(RBUF)  /* Recommended buffer size */
CNT,  EQ(PCNT)  /* Play counter */
COM,  EQ(COMM)  /* Comments */
CRA,  EQ(AENC)  /* Audio encryption */
CRM,  OBSOLETE  /* Encrypted meta frame [obsolete] */
EQU,  OBSOLETE  /* Equalization [obsolete] */
EQUA, OBSOLETE  /* Equalization [obsolete] */
ETC,  EQ(ETCO)  /* Event timing codes */
GEO,  EQ(GEOB)  /* General encapsulated object */
IPL,  EQ(TIPL)  /* Involved people list */
IPLS, EQ(TIPL)  /* Involved people list */
LNK,  EQ(LINK)  /* Linked information */
MCI,  EQ(MCDI)  /* Music CD identifier */
MLL,  EQ(MLLT)  /* MPEG location lookup table */
PIC,  TX(APIC)  /* Attached picture */
POP,  EQ(POPM)  /* Popularimeter */
REV,  EQ(RVRB)  /* Reverb */
RVA,  EQ(RVAD)  /* Relative volume adjustment [obsolete] */
RVAD, OBSOLETE  /* Relative volume adjustment [obsolete] */
SLT,  EQ(SYLT)  /* Synchronised lyric/text */
STC,  EQ(SYTC)  /* Synchronised tempo codes */
TAL,  EQ(TALB)  /* Album/movie/show title */
TBP,  EQ(TBPM)  /* BPM (beats per minute) */
TCM,  EQ(TCOM)  /* Composer */
TCO,  TX(TCON)  /* Content type */
TCON, TX(TCON)  /* Content type */
TCR,  EQ(TCOP)  /* Copyright message */
TDA,  EQ(TDAT)  /* Date [obsolete] */
TDAT, OBSOLETE  /* Date [obsolete] */
TDY,  EQ(TDLY)  /* Playlist delay */
TEN,  EQ(TENC)  /* Encoded by */
TFT,  EQ(TFLT)  /* File type */
TIM,  EQ(TIME)  /* Time [obsolete] */
TIME, OBSOLETE  /* Time [obsolete] */
TKE,  EQ(TKEY)  /* Initial key */
TLA,  EQ(TLAN)  /* Language(s) */
TLE,  EQ(TLEN)  /* Length */
TMT,  EQ(TMED)  /* Media type */
TOA,  EQ(TOPE)  /* Original artist(s)/performer(s) */
TOF,  EQ(TOFN)  /* Original filename */
TOL,  EQ(TOLY)  /* Original lyricist(s)/text writer(s) */
TOR,  EQ(TDOR)  /* Original release year [obsolete] */
TORY, EQ(TDOR)  /* Original release year [obsolete] */
TOT,  EQ(TOAL)  /* Original album/movie/show title */
TP1,  EQ(TPE1)  /* Lead performer(s)/soloist(s) */
TP2,  EQ(TPE2)  /* Band/orchestra/accompaniment */
TP3,  EQ(TPE3)  /* Conductor/performer refinement */
TP4,  EQ(TPE4)  /* Interpreted, remixed, or otherwise modified by */
TPA,  EQ(TPOS)  /* Part of a set */
TPB,  EQ(TPUB)  /* Publisher */
TRC,  EQ(TSRC)  /* ISRC (international standard recording code) */
TRD,  OBSOLETE  /* Recording dates [obsolete] */
TRDA, OBSOLETE  /* Recording dates [obsolete] */
TRK,  EQ(TRCK)  /* Track number/position in set */
TSI,  OBSOLETE  /* Size [obsolete] */
TSIZ, OBSOLETE  /* Size [obsolete] */
TSS,  EQ(TSSE)  /* Software/hardware and settings used for encoding */
TT1,  EQ(TIT1)  /* Content group description */
TT2,  EQ(TIT2)  /* Title/songname/content description */
TT3,  EQ(TIT3)  /* Subtitle/description refinement */
TXT,  EQ(TEXT)  /* Lyricist/text writer */
TXX,  EQ(TXXX)  /* User defined text information frame */
TYE,  EQ(TYER)  /* Year [obsolete] */
TYER, OBSOLETE  /* Year [obsolete] */
UFI,  EQ(UFID)  /* Unique file identifier */
ULT,  EQ(USLT)  /* Unsynchronised lyric/text transcription */
WAF,  EQ(WOAF)  /* Official audio file webpage */
WAR,  EQ(WOAR)  /* Official artist/performer webpage */
WAS,  EQ(WOAS)  /* Official audio source webpage */
WCM,  EQ(WCOM)  /* Commercial information */
WCP,  EQ(WCOP)  /* Copyright/legal information */
WPB,  EQ(WPUB)  /* Publishers official webpage */
WXX,  EQ(WXXX)  /* User defined URL link frame */
TCP,  EQ(TCMP)  /* non-standard iTunes compilation flag */
TST,  EQ(TSOT)  /* non-standard iTunes track sort */
TSA,  EQ(TSOA)  /* non-standard iTunes album sort */
TSP,  EQ(TSOP)  /* non-standard iTunes artist sort */
TS2,  EQ(TSO2), /* non-standard iTunes album artist sort */
TSC,  EQ(TSOC), /* non-standard iTunes composer sort */
%%
