#!perl

our $AUTHORITY = 'cpan:PERLANCAR'; # AUTHORITY
our $DATE = '2020-08-22'; # DATE
our $DIST = 'App-DiffDocText'; # DIST
our $VERSION = '0.001'; # VERSION

use 5.010001;
use strict;
use warnings;
use Log::ger;
#use Log::ger::Screen;

use Getopt::Long;

my @files;
Getopt::Long::Configure("gnu_getopt", "no_ignore_case", "pass_through");

my @diff_argv;
my $code_push_opt     = sub { my ($cb, $optval) = @_; my $optname = $cb->name; push @diff_argv, (length($optname) > 1 ? "--" : "-").$optname };
my $code_push_opt_val = sub { my ($cb, $optval) = @_; my $optname = $cb->name; push @diff_argv, (length($optname) > 1 ? "--" : "-").$optname, $optval };
my $fail;

GetOptions(
    normal => $code_push_opt,
    'brief|q' => $code_push_opt,
    'report-identical-files|s' => $code_push_opt_val,
    'c' => $code_push_opt,
    'context|C' => $code_push_opt,
    'u' => $code_push_opt,
    'unified|U' => $code_push_opt,
    'ed|e' => $code_push_opt,
    'rcs|n' => $code_push_opt,
    'side-by-side|y' => $code_push_opt,
    'width|W=i' => $code_push_opt_val,
    'left-column' => $code_push_opt,
    'suppress-common-lines' => $code_push_opt,
    'show-c-function|p' => $code_push_opt,
    'show-function-line|F=s' => $code_push_opt_val,
    'label=s' => $code_push_opt_val,
    'expand-tabs|t' => $code_push_opt,
    'initial-tab|T' => $code_push_opt,
    'tabsize=i' => $code_push_opt_val,
    'suppress-blank-empty' => $code_push_opt,
    'paginate|l' => $code_push_opt,
    'recursive|r' => $code_push_opt,
    'new-file|N' => $code_push_opt,
    'unidirectional-new-file' => $code_push_opt,
    'ignore-file-name-case!' => $code_push_opt,
    'exclude|x=s' => $code_push_opt_val,
    'exclude-from|X=s' => $code_push_opt_val, # filename
    'starting-file|S' => $code_push_opt_val, # filename
    'from-file=s' => $code_push_opt_val, # filename
    'to-file=s' => $code_push_opt_val, # filename
    'ignore-case|i' => $code_push_opt,
    'ignore-tab-expansion|E' => $code_push_opt,
    'ignore-trailing-space|Z' => $code_push_opt,
    'ignore-space-change|b' => $code_push_opt,
    'ignore-all-space|w' => $code_push_opt,
    'ignore-blank-lines|B' => $code_push_opt,
    'ignore-matching-lines|I=s' => $code_push_opt_val,
    'text|a' => $code_push_opt,
    'strip-trailing-cr' => $code_push_opt,
    'ifdef|D=s' => $code_push_opt_val,
    'GTYPE-group-format=s' => $code_push_opt_val,
    'line-format=s' => $code_push_opt_val,
    'LTYPE-line-format=s' => $code_push_opt_val,
    'minimal|d' => $code_push_opt,
    'horizon-lines=i' => $code_push_opt_val,
    'speed-large-files' => $code_push_opt,
    'help' => $code_push_opt,
    'version|v' => $code_push_opt,

    '<>' => sub {
        my $filename = $_[0];
        unless (-f $filename) {
            warn "diff-doc-text: No such file or not a file: '$filename'\n";
            $fail++;
            return;
        }

        require App::OfficeUtils;
        my $res = App::OfficeUtils::officewp2txt(
            input_file => $filename,
            fmt => 1,
            return_output_file => 1,
        );
        my $file;
        if ($res->[0] == 304) {
            $file = $_[0];
        } elsif ($res->[0] == 200) {
            $file = $res->[2];
        } else {
            die "Can't convert doc '$_[0]' to text: $res->[0] - $res->[1]";
        }
        push @diff_argv, $file;
    },
);

exit 1 if $fail;

require File::Which;
my $diff_cmd =
    $ENV{DIFF_DOC_TEXT_DIFF_CMD} //
    (File::Which::which("diffwc") ? "diffwc" : undef) // "diff";

require IPC::System::Options;
IPC::System::Options::system(
    {log=>1},
    $diff_cmd, @diff_argv,
);

# ABSTRACT: Diff the text of two Office word-processor documents (.doc, .docx, .odt, etc)
# PODNAME: diff-doc-text

__END__

=pod

=encoding UTF-8

=head1 NAME

diff-doc-text - Diff the text of two Office word-processor documents (.doc, .docx, .odt, etc)

=head1 VERSION

This document describes version 0.001 of diff-doc-text (from Perl distribution App-DiffDocText), released on 2020-08-22.

=head1 SYNOPSIS

Use like you would use the Unix command B<diff>:

 % diff-doc-text [options] <FILE>...

=head1 DESCRIPTION

This is a wrapper for the Unix command B<diff>. It assumes each file is an
olffice word-processor documents (.doc, .docx, .odt, etc) and tries to convert
it to plaintext first. It then pass the converted files to C<diff>.

=head1 DIFF_DOC_TEXT_DIFF_CMD

String. Can be used to set path to diff command. The defaultl is L<diffwc> if
available in PATH, or C<diff>.

=head1 ENVIRONMENT

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-DiffDocText>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-DiffDocText>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-DiffDocText>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 SEE ALSO

Unix command L<diff>.

L<App::OfficeUtils> and the included CLI L<doc2txt>.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2020 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
