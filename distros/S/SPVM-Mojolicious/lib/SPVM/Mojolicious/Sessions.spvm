# Copyright (c) 2025 Yuki Kimoto
# MIT License

class Mojolicious::Sessions {
  version_from Mojolicious;
  
  use JSON;
  use Mojo::Util;
  use Mojolicious::Sessions::Callback::Serialize;
  use Mojolicious::Sessions::Callback::Deserialize;
  use Mojolicious::Controller;
  
  # Fields
  has cookie_domain : rw string;
  
  has secure : rw byte;
  
  has cookie_name : rw string;
  
  has cookie_path : rw string;
  
  has default_expiration : rw int;
  
  has samesite : rw string;
  
  # Class Methods
  static method new : Mojolicious::Sessions () {
    
    my $self = new Mojolicious::Sessions;
    
    $self->{cookie_name} = "mojolicious";
    
    $self->{cookie_path} = "/";
    
    $self->{default_expiration} = 3600;
    
    $self->{samesite} = "Lax";
    
    return $self;
  }
  
  # Instance Methods
  method load : void ($c : Mojolicious::Controller) {
    
    my $value = $c->signed_cookie($self->cookie_name);
    
    unless ($value) {
      return;
    }
    
    $value = copy $value;
    Re->s((mutable string)$value, ["-", "g"], "=");
    
    my $session = (Hash)JSON->new->decode(Mojo::Util->b64_decode($value));
    unless ($session) {
      return;
    }
    
    my $exists_expiration = $session->exists("expiration");
    
    my $exists_expires = $session->exists("expires");
    
    # "expiration" value is inherited
    my $expiration = $exists_expiration ? $session->{"expiration"}->(int) : $self->default_expiration;
    
    my $expires =  $session->{"expires"}->(long);
    $session->delete("expires");
    
    if (!$expires && $expiration) {
      return;
    }
    
    
    if ($exists_expires && $expires <= Sys->time) {
      return;
    }
    
    my $session_keys = $session->keys;
    
    my $session_keys_length = @$session_keys;
    
    $c->set_mojo_active_session($session_keys_length);
    
    unless ($session_keys_length) {
      return;
    }
    
    $c->set_mojo_session($session);
    
    if ($session->get("new_flash")) {
      $session->set(flash => $session->delete("new_flash"));
    }
  }
  

  method store : void ($c : Mojolicious::Controller) {
    
    my $stash = $c->stash_h;
    
    # Make sure session was active
    my $session = $c->session_h;
    unless ($session) {
      return;
    }
    
    unless ($session->keys_length || $c->mojo_active_session) {
      return;
    }
    
    # Don't reset flash for static files
    my $old = $session->delete("flash");
    if ($c->mojo_static) {
      $session->{"new_flash"} = $old;
    }
    
    my $new_flash = $session->{"new_flash"}->(Hash);
    unless ($new_flash && $new_flash->keys_length) {
      $session->delete("new_flash");
    }
    
    # Generate "expires" value from "expiration" if necessary
    my $expiration = $session->exists("expiration") ? $session->{"expiration"}->(int) : $self->default_expiration;
    my $default = $session->{"expires"};
    $session->delete("expires");
    
    if ($expiration || $default) {
      if ($default) {
        $session->{"expires"} = $default;
      }
      elsif ($expiration) {
        $session->{"expires"} = Sys->time + $expiration;
      }
    }
    
    my $value = Mojo::Util->b64_encode(JSON->new->encode($session), "");
    Re->s((mutable string)$value, ["=", "g"], "-");
    my $options = {
      domain   => $self->cookie_domain,
      expires  => $session->{"expires"},
      httponly => 1,
      path     => $self->cookie_path,
      samesite => $self->samesite,
      secure   => $self->secure
    };
    
    $c->set_signed_cookie($self->cookie_name, $value, $options);
  }
  
}
