#!perl

our $DATE = '2019-06-20'; # DATE
our $VERSION = '0.724'; # VERSION

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{pmstripper} = {
    v => 1.1,
    summary => 'Strip Perl module source codes using Perl::Stripper',
    args => {
        module => $App::PMUtils::arg_module_multiple,
    },
};
sub pmstripper {
    require Module::Path::More;
    require Perl::Stripper;

    my %args = @_;
    my $mods = $args{module};
    return [400, "Please specify at least one module"] unless @$mods;

    my $stripper = Perl::Stripper->new(
        maintain_linum => $args{stripper_maintain_linum},
        strip_ws       => $args{stripper_ws},
        strip_comment  => $args{stripper_comment},
        strip_pod      => $args{stripper_pod},
        strip_log      => $args{stripper_log},
    );

    my $has_success;
    my $has_error;
    for my $mod (@$mods) {
        $mod =~ s!/!::!g;
        my $path = Module::Path::More::module_path(module=>$mod) or do {
            warn "pmstripper: No such module '$mod'";
            $has_error++;
            next;
        };
        open my $fh, "<", $path or do {
            warn "pmstripper: Can't open '$path': $!\n";
            $has_error++;
            next;
        };
        local $/;
        print $stripper->strip(~~<$fh>);
        close $fh;
        $has_success++;
    }

    if ($has_error) {
        if ($has_success) {
            return [207, "Some modules failed"];
        } else {
            return [500, "All modules failed"];
        }
    } else {
        return [200, "All modules OK"];
    }
}

require PERLANCAR::AppUtil::PerlStripper; PERLANCAR::AppUtil::PerlStripper::_add_stripper_args_to_meta($SPEC{pmstripper});
# since we always strip, we don't need these
for ($SPEC{pmstripper}) {
    delete $_->{args}{stripper};
    delete $_->{args_rels};
}

Perinci::CmdLine::Any->new(
    url => '/main/pmstripper',
)->run;

# ABSTRACT: Strip Perl module source codes using Perl::Stripper
# PODNAME: pmstripper

__END__

=pod

=encoding UTF-8

=head1 NAME

pmstripper - Strip Perl module source codes using Perl::Stripper

=head1 VERSION

This document describes version 0.724 of pmstripper (from Perl distribution App-PMUtils), released on 2019-06-20.

=head1 SYNOPSIS

Basic usage:

 % pmstripper Some::Module Another::Module

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--module-json>=I<s>

See C<--module>.

=item B<--module>=I<s@>*

Can be specified multiple times.

=back

=head2 Configuration options

=over

=item B<--config-path>=I<filename>, B<-c>

Set path to configuration file.

=item B<--config-profile>=I<s>, B<-P>

Set configuration profile to use.

=item B<--no-config>, B<-C>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Stripping options

=over

=item B<--no-stripper-comment>

Set strip_comment=0 (don't strip comments) in Perl::Stripper.

=item B<--no-stripper-pod>

Set strip_pod=0 (don't strip POD) in Perl::Stripper.

=item B<--no-stripper-ws>

Set strip_ws=0 (don't strip whitespace) in Perl::Stripper.

=item B<--stripper-log>

Set strip_log=1 (strip log statements) in Perl::Stripper.

=item B<--stripper-maintain-linum>

Set maintain_linum=1 in Perl::Stripper.

=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C pmstripper pmstripper

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pmstripper 'p/*/`pmstripper`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration files. Configuration files are in the format of L<IOD>, which is basically INI with some extra features.

By default, these names are searched for configuration filenames (can be changed using C<--config-path>): F<~/.config/pmstripper.conf>, F<~/pmstripper.conf>, or F</etc/pmstripper.conf>.

All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> or C<[SOMESECTION profile=SOMENAME]>. Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=NAME ...]> or C<[SOMESECTION program=NAME]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]> or C<[SOMESECTION env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]> or C<[SOMESECTION env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]> or C<[SOMESECTION env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]> or C<[SOMESECTION env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 format (see --format)
 module (see --module)
 naked_res (see --naked-res)
 stripper_comment (see --no-stripper-comment)
 stripper_log (see --stripper-log)
 stripper_maintain_linum (see --stripper-maintain-linum)
 stripper_pod (see --no-stripper-pod)
 stripper_ws (see --no-stripper-ws)

=head1 ENVIRONMENT

=head2 PMSTRIPPER_OPT => str

Specify additional command-line options.

=head1 FILES

F<~/.config/pmstripper.conf>

F<~/pmstripper.conf>

F</etc/pmstripper.conf>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PMUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2019, 2018, 2017, 2016, 2015, 2014 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
