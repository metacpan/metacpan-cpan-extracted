#!/usr/bin/env perl

# Create the colormap datastructure from the Brewer data
# Freely Available (GPL'd) Available:
#
#    http://www.personal.psu.edu/cab38/ColorBrewer/ColorBrewer_RGB.html
#
#    http://www.personal.psu.edu/cab38/ColorBrewer/ColorBrewer_all_schemes_RGBonly3.XLS
#

use Term::Colormap qw( rgb2color );
my $VERSION = $Term::Colormap::VERSION;

use warnings;
use strict;

my $filename = $ARGV[0] || '';

die "usage: $0 brewer.csv"
    unless -f $filename;

open my $fh, $filename
    or die "unable to open '$filename' : $!";

my @header;
my $colormaps;
my $colors = [];
while (my $line = <$fh>) {
    chomp($line);
    my @fields = split /,/, $line;    
    last unless scalar @fields;

    if ($. == 1) {
        @header = @fields;
        next;
    }
    if ( $fields[0] =~ m|\w| ) {
        # New Colormap

        # Store the last one we created
        push @$colormaps, $colors
            if scalar @$colors; 

        $colors = [];
    }

    # Key / value pairs of the colors in the colormap
    push @$colors, { map { $header[$_] => $fields[$_] } ( 0 .. $#fields ) };
    
}

# Store the last one
push @$colormaps, $colors
    if scalar @$colors; 

# Output the datastructure
print <<'EOHEAD';
package Term::Colormap::Brewer;

use Term::Colormap qw( add_mapping );

EOHEAD
print 'our $VERSION' . " = '" . $VERSION .  "';\n\n";

my $generated_header = "### Generated by running $0 $filename\n";
print $generated_header;

my @pod_desc;
my $lastschemetype = '';    
for my $colormap ( @$colormaps ) {
    my $c = $colormap->[0];

    my $colorname   = $c->{ColorName};
    my $numofcolors = $c->{NumOfColors};
    my $schemetype  = $c->{SchemeType} || $lastschemetype; # Empty after the first
    $lastschemetype = $schemetype;

    my $name = lc(join('-', $colorname, $numofcolors));
    my $desc = join(', ',
                    "SchemeType = $schemetype",
                    "ColorName = $colorname",
                    "NumOfColors = $numofcolors");

    print '# ' . $desc . "\n";
    print "add_mapping('$name', [ ";
    for my $color ( @$colormap ) {
        my ($rgb) = sprintf("%02x%02x%02x", $color->{R}, $color->{G}, $color->{B});
        my $color_id = rgb2color($rgb);
        print $color_id . ', ';
    }
    print ']);' . "\n\n";

    push @pod_desc, sprintf("%-12s - %s, %s, %d colors",
                            $name, $schemetype, $colorname, $numofcolors); 
}

print $generated_header;
print "\n";

print <<'EOPOD';
=head1 NAME Term::Colormap::Brewer

=head1 VERSION

Version 0.01

=head1 SYNOPSIS

Provide the Brewer colormaps.

    use Term::Colormap::Brewer qw( colormap colorbar print_colored );

    my $accent = colormap('accent-3');

    colorbar($accent);

    print_colored( $accent->[3], "some color" )';

=head1 CITATION

Colors from www.ColorBrewer.org by Cynthia A. Brewer, Geography, Pennsylvania State University.

=head1 AVAILABLE COLORMAPS

EOPOD

for my $pod (@pod_desc) {
    print "    $pod\n";
}

print <<'EOPOD';

=head1 AUTHOR

Felix Tubiana, C<< <felixtubiana at gmail.com> >>

=head1 BUGS

Please report any bugs or feature requests to C<bug-term-colormap at rt.cpan.org>,
or through the web interface at L<http://rt.cpan.org/NoAuth/ReportBug.html?Queue=Term-Colormap>.
I will be notified, and then you'll automatically be notified of progress on your bug
as I make changes.

=head1 SUPPORT

You can find documentation for this module with the perldoc command.

    perldoc Term::Colormap::Brewer

You can also look for information at:

=over 4

=item * RT: CPAN's request tracker (report bugs here)

L<http://rt.cpan.org/NoAuth/Bugs.html?Dist=Term-Colormap>

=item * AnnoCPAN: Annotated CPAN documentation

L<http://annocpan.org/dist/Term-Colormap>

=item * CPAN Ratings

L<http://cpanratings.perl.org/d/Term-Colormap>

=item * Search CPAN

L<http://search.cpan.org/dist/Term-Colormap/>

=back


=head1 ACKNOWLEDGEMENTS

Inspired by Term::ANSIColor


=head1 LICENSE AND COPYRIGHT

Copyright 2017 Felix Tubiana.

This program is free software; you can redistribute it and/or modify it
under the terms of the the Artistic License (2.0). You may obtain a
copy of the full license at:

L<http://www.perlfoundation.org/artistic_license_2_0>

Any use, modification, and distribution of the Standard or Modified
Versions is governed by this Artistic License. By using, modifying or
distributing the Package, you accept this license. Do not use, modify,
or distribute the Package, if you do not accept this license.

If your Modified Version has been derived from a Modified Version made
by someone other than you, you are nevertheless required to ensure that
your Modified Version complies with the requirements of this license.

This license does not grant you the right to use any trademark, service
mark, tradename, or logo of the Copyright Holder.

This license includes the non-exclusive, worldwide, free-of-charge
patent license to make, have made, use, offer to sell, sell, import and
otherwise transfer the Package with respect to any patent claims
licensable by the Copyright Holder that are necessarily infringed by the
Package. If you institute patent litigation (including a cross-claim or
counterclaim) against any party alleging that the Package constitutes
direct or contributory patent infringement, then this Artistic License
to you shall terminate on the date that such litigation is filed.

Disclaimer of Warranty: THE PACKAGE IS PROVIDED BY THE COPYRIGHT HOLDER
AND CONTRIBUTORS "AS IS' AND WITHOUT ANY EXPRESS OR IMPLIED WARRANTIES.
THE IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
PURPOSE, OR NON-INFRINGEMENT ARE DISCLAIMED TO THE EXTENT PERMITTED BY
YOUR LOCAL LAW. UNLESS REQUIRED BY LAW, NO COPYRIGHT HOLDER OR
CONTRIBUTOR WILL BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, OR
CONSEQUENTIAL DAMAGES ARISING IN ANY WAY OUT OF THE USE OF THE PACKAGE,
EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

=cut

EOPOD

exit 0;
