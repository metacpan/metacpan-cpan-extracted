
MACRO(lcfg_translate)

[%+ FOREACH file IN trans.keys.sort %]
CONFIGURE_FILE("[% file %]" "[% trans.item(file) %]" @ONLY)
[%+ END +%]

ENDMACRO(lcfg_translate)

MACRO(lcfg_add_comp_script compname)

  if( IS_ABSOLUTE ${compname} )
    SET(comppath ${compname})
  elseif( EXISTS "${CMAKE_BINARY_DIR}/${compname}")
    SET(comppath "${CMAKE_BINARY_DIR}/${compname}")
  else()
    SET(comppath "${CMAKE_SOURCE_DIR}/${compname}")
  endif()

  # This is marked as optional as 'virtual' components do not have any
  # code, just a schema

  install(PROGRAMS ${comppath} DESTINATION ${LCFGCOMP} OPTIONAL)

ENDMACRO(lcfg_add_comp_script)

MACRO(lcfg_add_schema schema_name schema_ver)

  SET(schemafile ${schema_name}.def)

  if(EXISTS "${CMAKE_BINARY_DIR}/${schemafile}")
    SET(schemapath "${CMAKE_BINARY_DIR}/${schemafile}")
  else()
    SET(schemapath "${CMAKE_SOURCE_DIR}/${schemafile}")
  endif()

  # Schema file is now optional

  if(EXISTS ${schemapath})

    STRING(LENGTH ${schema_ver} schema_length)
    IF(${schema_length} GREATER 0)
      SET(deffile ${schema_name}-${schema_ver}.def)
    ELSE(${schema_length} GREATER 0)
      SET(deffile ${schema_name}.def)
    ENDIF(${schema_length} GREATER 0)

    INSTALL(FILES ${schemapath}
            DESTINATION ${LCFGCLIENTDEF}
            RENAME ${deffile})

    INSTALL(FILES ${schemapath}
            DESTINATION ${LCFGSERVERDEF}
            RENAME ${deffile}
            COMPONENT defaults)

  endif()

ENDMACRO(lcfg_add_schema)

FIND_PROGRAM(PODSTRIP podselect)

MACRO(pod_strip in out)

  if(PODSTRIP)

    # Calling podselect like this will happily generate empty files when
    # the input does not contain any POD. Attempt to read a single byte
    # to see if the output file has any contents. Remove if the file is
    # empty.

    EXECUTE_PROCESS(COMMAND ${PODSTRIP} ${in} OUTPUT_FILE ${out})

    if(EXISTS ${out})
      file(STRINGS ${out} tmpstring LIMIT_INPUT 1)
      if ( NOT tmpstring )
        file(REMOVE ${out})
      endif()
    endif()

  endif(PODSTRIP)

ENDMACRO(pod_strip)

MACRO(lcfg_add_man manfile section)

  if( IS_ABSOLUTE ${manfile} )
    SET(manpath ${manfile})
  elseif( EXISTS "${CMAKE_BINARY_DIR}/${manfile}")
    SET(manpath "${CMAKE_BINARY_DIR}/${manfile}")
  else()
    SET(manpath "${CMAKE_SOURCE_DIR}/${manfile}")
  endif()

  INSTALL(FILES ${manpath} DESTINATION ${LCFGMAN}/man${section})

ENDMACRO(lcfg_add_man)

FIND_PROGRAM(POD2MAN pod2man)

MACRO(lcfg_pod2man podfile manfile section)
  IF(NOT POD2MAN)
    message(FATAL ERROR "Need pod2man installed to generate manual files")
  ENDIF(NOT POD2MAN)

  if( IS_ABSOLUTE ${podfile} )
    SET(podpath ${podfile})
  elseif( EXISTS "${CMAKE_BINARY_DIR}/${podfile}")
    SET(podpath "${CMAKE_BINARY_DIR}/${podfile}")
  else()
    SET(podpath "${CMAKE_SOURCE_DIR}/${podfile}")
  endif()

  if(NOT EXISTS ${podpath})
    MESSAGE(FATAL ERROR "Could not find pod file ${podfile} to generate manual file")
  endif()

  SET(outfile "${CMAKE_BINARY_DIR}/${manfile}.${section}")

  if(LCFG_GROUP)
    EXECUTE_PROCESS(COMMAND ${POD2MAN} --section="${section}" --release="Release: ${LCFG_VERSION}" --center="${LCFG_GROUP}" --date="${LCFG_DATE}" ${podpath} ${outfile})
  else()
    EXECUTE_PROCESS(COMMAND ${POD2MAN} --section="${section}" --release="Release: ${LCFG_VERSION}" --date="${LCFG_DATE}" ${podpath} ${outfile})
  endif()

  if(EXISTS ${outfile})
    lcfg_add_man(${outfile} ${section})
  endif()

ENDMACRO(lcfg_pod2man)

MACRO(lcfg_add_pod podfile manfile section)

  if( IS_ABSOLUTE ${podfile} )
    SET(podpath ${podfile})
  elseif( EXISTS "${CMAKE_BINARY_DIR}/${podfile}")
    SET(podpath "${CMAKE_BINARY_DIR}/${podfile}")
  else()
    SET(podpath "${CMAKE_SOURCE_DIR}/${podfile}")
  endif()

  install(FILES ${podpath} DESTINATION ${LCFGPOD})
  lcfg_pod2man(${podpath} ${manfile} ${section})

ENDMACRO(lcfg_add_pod)

MACRO(lcfg_add_comp_pod compname)

  SET(podfile "${compname}.pod")
  SET(manfile "lcfg-${compname}")

  lcfg_add_pod(${podfile} ${manfile} ${MANSECT})

ENDMACRO(lcfg_add_comp_pod)

SET(PERL_LIBSECT 3 CACHE STRING "Manual section for Perl libraries")

MACRO(lcfg_add_perl_module file modpath)

  STRING( REGEX REPLACE "\\.pm" "" modname ${modpath})
  STRING( REGEX REPLACE "/" "::" modname ${modname} )

  SET(podfile "${CMAKE_BINARY_DIR}/${modname}.pod")

  pod_strip(${file} ${podfile})
  if(EXISTS ${podfile})
    lcfg_add_pod(${podfile} ${modname} ${PERL_LIBSECT})
  endif()

  GET_FILENAME_COMPONENT(dirname ${modpath} PATH)

  INSTALL(FILES ${file}
          DESTINATION "${PERL_LIBDIR}/${dirname}")

ENDMACRO(lcfg_add_perl_module)

MACRO(lcfg_add_perl_tree dir)

  # Need to search both the binary and source directories

  FILE( GLOB_RECURSE perlmods
        RELATIVE "${CMAKE_BINARY_DIR}/${dir}"
        "${CMAKE_BINARY_DIR}/${dir}/*.pm")
  FOREACH(perlmod ${perlmods})
    lcfg_add_perl_module("${CMAKE_BINARY_DIR}/${dir}/${perlmod}" ${perlmod})
  ENDFOREACH(perlmod)

  IF( NOT ${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR} )
    FILE( GLOB_RECURSE perlmods
          RELATIVE "${CMAKE_SOURCE_DIR}/${dir}"
          "${CMAKE_SOURCE_DIR}/${dir}/*.pm")
    FOREACH(perlmod ${perlmods})
      lcfg_add_perl_module("${CMAKE_SOURCE_DIR}/${dir}/${perlmod}" ${perlmod})
    ENDFOREACH(perlmod)
  ENDIF( NOT ${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR} )

ENDMACRO(lcfg_add_perl_tree)

MACRO(lcfg_add_program location binfile mansect)

  if(${ARGC} GREATER 3)
    if (${ARGV3} STREQUAL "podstrip")
      set(podfile "${CMAKE_BINARY_DIR}/${binfile}.pod")
      pod_strip(${binfile} ${podfile})
      lcfg_add_pod(${podfile} ${binfile} ${mansect})
    endif()
  endif()

  if( IS_ABSOLUTE ${binfile} )
    SET(binpath ${binfile})
  elseif( EXISTS "${CMAKE_BINARY_DIR}/${binfile}")
    SET(binpath "${CMAKE_BINARY_DIR}/${binfile}")
  else()
    SET(binpath "${CMAKE_SOURCE_DIR}/${binfile}")
  endif()

  INSTALL(PROGRAMS ${binpath} DESTINATION ${location})

ENDMACRO(lcfg_add_program)

MACRO(lcfg_add_bin binfile)
  lcfg_add_program(${LCFGBIN} ${binfile} 1 ${ARGV1})
ENDMACRO(lcfg_add_bin)

MACRO(lcfg_add_sbin binfile)
  lcfg_add_program(${LCFGSBIN} ${binfile} 8 ${ARGV1})
ENDMACRO(lcfg_add_sbin)

MACRO(lcfg_add_logrotate)

  if( EXISTS "${CMAKE_BINARY_DIR}/logrotate")
    SET(logrotpath "${CMAKE_BINARY_DIR}/logrotate")
  else()
    SET(logrotpath "${CMAKE_SOURCE_DIR}/logrotate")
  endif()

  # logrotate template file is optional

  install(FILES ${logrotpath} DESTINATION "${LCFGDATA}/${LCFG_NAME}" OPTIONAL)

ENDMACRO(lcfg_add_logrotate)

MACRO(lcfg_add_template)
  INSTALL(FILES ${ARGV} DESTINATION ${LCFG_TMPLDIR})
ENDMACRO(lcfg_add_template)

MACRO(lcfg_add_templates)

  # Allow non-standard templates directory

  if(${ARGC} GREATER 0)
    SET(srcdir "${CMAKE_CURRENT_SOURCE_DIR}/${ARGV0}")
    SET(bindir "${CMAKE_CURRENT_BINARY_DIR}/${ARGV0}")
  else()
    SET(srcdir "${CMAKE_CURRENT_SOURCE_DIR}/templates")
    SET(bindir "${CMAKE_CURRENT_BINARY_DIR}/templates")
  endif()

  # Allow non-standard destination directory

  IF(${ARGC} GREATER 1)
    SET(destination "${ARGV1}")
  ELSE(${ARGC} GREATER 1)
    SET(destination "${LCFG_TMPLDIR}")
  ENDIF(${ARGC} GREATER 1)

  # New-style templates directory, can contain sub-directories

  FILE(GLOB_RECURSE templates "${srcdir}/*")

  FOREACH(template ${templates})
    get_filename_component(basename ${template} NAME)
    if(${basename} MATCHES "(.+)\\.c?in$")
      # Handled separately
    else()
      configure_file(${template} "${bindir}/${basename}" COPYONLY)
    endif()
  ENDFOREACH(template)

  install(DIRECTORY ${bindir}/ DESTINATION ${destination} OPTIONAL)

  # Old-style single template file named 'template' in the top-level
  #  of the project directory.

  install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/template" "${CMAKE_CURRENT_BINARY_DIR}/template" DESTINATION ${destination} OPTIONAL)

ENDMACRO(lcfg_add_templates)

MACRO(lcfg_add_nagios_module modpath)

  get_filename_component(modname ${modpath} NAME)
  lcfg_add_perl_module(${modpath} "LCFG/Monitoring/Nagios/Translators/${modname}")

ENDMACRO(lcfg_add_nagios_module)

MACRO(lcfg_add_nagios_support)

  file(GLOB nagios_mods "${CMAKE_CURRENT_SOURCE_DIR}/nagios/*.pm" "${CMAKE_CURRENT_BINARY_DIR}/nagios/*.pm")

  foreach(nagios_mod ${nagios_mods})
    lcfg_add_nagios_module(${nagios_mod})
  endforeach(nagios_mod ${nagios_mods})

ENDMACRO(lcfg_add_nagios_support)

MACRO(lcfg_add_meta)

  install(FILES README ChangeLog
          DESTINATION share/doc/${LCFG_FULLNAME}-${LCFG_VERSION}
          OPTIONAL)

ENDMACRO(lcfg_add_meta)

MACRO(lcfg_add_component compname)

  lcfg_translate()
  lcfg_add_comp_script(${compname})
  lcfg_add_comp_pod(${compname})
  lcfg_add_schema(${compname} ${LCFG_SCHEMA})
  lcfg_add_logrotate()
  lcfg_add_templates()
  lcfg_add_nagios_support()

  # This is a directory used by components to store generated files
  # Lots of components need it and it is harmless to create it for
  # those which do not.

  install(DIRECTORY DESTINATION ${CONFIGDIR})

  # This is done here only for MacOSX as fedora/redhat do this in the
  # specfile.

  IF(${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
     lcfg_add_meta()
  ENDIF(${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")

ENDMACRO(lcfg_add_component)

