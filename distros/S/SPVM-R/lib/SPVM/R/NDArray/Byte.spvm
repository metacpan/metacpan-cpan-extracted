# Copyright (c) 2024 Yuki Kimoto
# MIT License

# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Byte extends R::NDArray {
  
  use R::NDArray::Int;
  
  # Fields
  method data : byte[] () {
    
    return (byte[])$self->{data};
  }
  
  # Class Mehtods
  static method new : R::NDArray::Byte ($options : object[] = undef) {
    
    my $self = new R::NDArray::Byte;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method create_default_data : byte[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new byte[$length];
    
    return $data;
  }
  
  method elem_to_string : string ($data : byte[], $data_index : int) {
    
    my $elem = $data->[$data_index];
    
    my $string = (string)(byte)$elem;
    
    return $string;
  }
  
  method elem_assign : void ($dist_data : byte[], $dist_data_index : int, $src_data : byte[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_clone : void ($dist_data : byte[], $dist_data_index : int, $src_data : byte[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_cmp : int ($data : byte[], $a_data_index : int, $b_data_index : int) {
    
    my $cmp = $data->[$a_data_index] <=> $data->[$b_data_index];
    
    return $cmp;
  }
  
  method elem_is_na : int ($data : byte[], $data_index : int) {
    
    my $elem_is_na = 0;
    
    return $elem_is_na;
  }
  
  method clone : R::NDArray::Byte ($shallow : int = 0) {
    return (R::NDArray::Byte)$self->SUPER::clone($shallow);
  }
  
  method slice : R::NDArray::Byte ($asix_indexes_product : R::NDArray::Int[]) {
    return (R::NDArray::Byte)$self->SUPER::slice($asix_indexes_product);
  }
  
  precompile method to_int_ndarray : R::NDArray::Int () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Int->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (int)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
}
