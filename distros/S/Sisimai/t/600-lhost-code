#!/usr/bin/env perl
package Sisimai::Lhost::Code;
use lib qw(./lib ./blib/lib);
use strict;
use warnings;

my $moduletest = sub {
    my $modulename = shift || return undef;
    my $isexpected = shift || return undef;
    my $privateset = shift || 0;
    my $onlydebugs = shift || 0;

    my $E = $modulename;    # Sendmail, Postfix, and so on.
    my $M = undef;          # Sisiimai::Lhost::Sendmail, Sisimai::ARF, and so on.
    my $v = undef;

    use Test::More;
    use JSON;
    use Module::Load;
    use Sisimai::Mail;
    use Sisimai::Fact;
    use Sisimai::Lhost;
    use Sisimai::Reason;
    use Sisimai::Address;

    my $lhostindex = Sisimai::Lhost->index; push @$lhostindex, 'ARF', 'RFC3464', 'RFC3834';
    my $isnotlhost = qr/\A(?:ARF|RFC3464|RFC3834)\z/;
    my $methodlist = ['DELIVERYSTATUS', 'INDICATORS', 'description', 'index', 'path'];
    my $alternates = {
        'Exchange2007' => ['Office365'],
        'Exim'         => ['MailRu', 'MXLogic'],
        'qmail'        => ['X4', 'Yahoo'],
        'RFC3464'      => [qw|
            Aol Amavis AmazonWorkMail Barracuda Bigfoot Facebook GSuite McAfee MessageLabs Outlook
            PowerMTA ReceivingSES SendGrid SurfControl Yandex X5
        |],
    };
    my $feedbackid = [
        "lhost-amazonses-05", "lhost-amazonses-06", "lhost-amazonses-07", "lhost-amazonses-08", 
        "arf-14", "lhost-einsundeins-03", "lhost-exchange2007-05", "lhost-exchange2007-06",
        "lhost-exim-61", "lhost-postfix-64", "rhost-franceptt-04", 
    ];

    my $jsonobject = JSON->new;
    my $emailindex = 0;
    my $nameprefix = '';
    my $reasonlist = [map { $_ = lc $_ } @{ Sisimai::Reason->index }];
    push @$reasonlist, 'delivered', 'feedback', 'undefined', 'vacation';

    if( $E =~ $isnotlhost ) {
        # ARF, RFC3464, ARF3834
        $M = sprintf("Sisimai::%s", $E);

    } else {
        my $calledfrom = [caller()]->[1];
        my $kindofhost = $calledfrom =~ /-rhost-/ ? 'rhost' : 'lhost';
        if( grep { $E eq $_ } $alternates->{'RFC3464'}->@* ) {
            # Removed Lhost moudles (Sisimai::RFC3464 can decode)
            $M = "Sisimai::RFC3464";

        } elsif( grep { $E eq $_ } $alternates->{'Exchange2007'}->@* ) {
            # Removed Lhost moudles (Sisimai::Lhost::Exchange2007 can decode)
            $M = "Sisimai::Lhost::Exchange2007";

        } elsif( grep { $E eq $_ } $alternates->{'Exim'}->@* ) {
            # Removed Lhost moudles (Sisimai::Lhost::Exim can decode)
            $M = "Sisimai::Lhost::Exim";

        } elsif( grep { $E eq $_ } $alternates->{'qmail'}->@* ) {
            # Removed Lhost moudles (Sisimai::Lhost::qmail can decode)
            $M = "Sisimai::Lhost::qmail";

        } else {
            # Sisimai::Lhost OR Sisimai::Rhost
            $M = sprintf("Sisimai::%s::%s", ucfirst $kindofhost, $E);
        }
        $nameprefix = $kindofhost.'-';
    }
    my $samplepath = $privateset ? sprintf("set-of-emails/private/%s%s", $nameprefix, lc $E) : 'set-of-emails/maildir/bsd';

    Module::Load::load $M;
    use_ok $M;
    can_ok $M, @$methodlist    if $M =~ /Sisimai::Lhost/;
    can_ok $M, ('inquire') unless $M =~ /Sisimai::Rhost/;
    is $M->inquire(undef),     undef unless $M =~ /Sisimai::Rhost/;
    is $M->inquire({} ,undef), undef unless $M =~ /Sisimai::Rhost/;

    PARSE_EACH_EMAIL: for my $e ( sort keys %$isexpected ) {
        # Open each email in set-of-emails/ directory
        my $cj = undef; # Current JSON/YAML string
        my $cf = undef; # Current sample email file
        my $cr = undef; # Current regular expression
        my $ct = undef; # Current text for displaying with -v option
        my $cv = undef; # Current value for test
        my $cx = $isexpected->{ $e };

        my $haveparsed = 0;

        if( $onlydebugs ) {
            # Debug mode
            $emailindex += 1;
            next unless int($onlydebugs) == int($e);
            ok $onlydebugs, sprintf("[%s] %s|DEBUG(%02d)", $e, $E, $onlydebugs);
        }

        if( $privateset ) {
            # Private sample: 01227-581a7c3e4f0c0664ff171969c34bf988.eml
            $cf = [glob(sprintf("./%s/%s-*.eml", $samplepath, $e))]->[0];

        } else {
            # Public sample: lhost-sendmail-59.eml
            $cf = $E =~ $isnotlhost
                ? $cf = sprintf("./%s/%s-%02d.eml", $samplepath, lc $E, int $e)
                : $cf = sprintf("./%s/%s%s-%02d.eml", $samplepath, $nameprefix, lc $E, int $e);
        }

        ok -f $cf, sprintf("[%s---] %s/email(path) = %s", $e, $E, $cf);
        ok -s $cf, sprintf("[%s---] %s/email(size) = %s", $e, $E, -s $cf);

        my $mailobject = Sisimai::Mail->new($cf);
        # next unless defined $mailobject;
        isa_ok $mailobject, 'Sisimai::Mail';

        READ_EACH_EMAIL: while( my $r = $mailobject->data->read ) {
            # Read messages in each email
            my $methodargs = { 'data' => $r, 'delivered' => 1, 'vacation' => 1, 'origin' => $cf };
            my $listoffact = Sisimai::Fact->rise($methodargs);

            unless( $listoffact ) {
                my $bf = '';
                my $be = '';

                if( $privateset ) {
                    $bf = [split('/', $cf, 4)]->[-1];
                    $be = 'private';

                } else {
                    $bf = [split('/', $cf)]->[-1];
                    $be = 'public';
                }
                $bf =~ s/[.]eml\z//;
                printf("\n[%s-00] %s need to be inspected\n", $e, $cf); next;
            }

            my $recipients = scalar @$listoffact;
            my $errorindex = 0;

            isa_ok $listoffact, 'ARRAY';
            ok $recipients, sprintf("[%s---] %s/including %d bounces", $e, $E, $recipients);

            while( my $rr = shift @$listoffact ) {
                # Test each Sisimai::Fact object
                isa_ok $rr, 'Sisimai::Fact';
                $errorindex += 1;

                ACTION: {
                    $cv = $rr->action;
                    $cr = qr/\A(?:delayed|delivered|expanded|failed|relayed)\z/;
                    $ct = sprintf("[%s-%02d] ->action =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                    if( $rr->reason eq 'feedback' || $rr->reason eq 'vacation' ) {
                        # "action" is empty when the value of "reason" is "feedback" OR "vacation"
                        is $cv, '', sprintf("%s %s", $ct, '');

                    } else {
                        # The value of "reason" is not "feedback"
                        ok $cv =~ $cr, sprintf("%s %s", $ct, $cr);
                    }
                }

                ADDRESSER: {
                    isa_ok $rr->addresser, 'Sisimai::Address';

                    $cv = $rr->addresser->address;
                    $cr = qr/\A.+[@][0-9A-Za-z._-]+[A-Za-z]+?\z/;
                    $ct = sprintf("[%s-%02d] ->addresser->", $e, $errorindex);

                    ok defined $rr->addresser->alias,   sprintf("%s%s = %s", $ct, 'alias', $rr->addresser->alias);
                    ok defined $rr->addresser->verp,    sprintf("%s%s = %s", $ct, 'verp',  $rr->addresser->verp);
                    ok defined $rr->addresser->name,    sprintf("%s%s = %s", $ct, 'name',  $rr->addresser->name);
                    ok defined $rr->addresser->comment, sprintf("%s%s = %s", $ct, 'comment', $rr->addresser->comment);
                    ok length  $rr->addresser->user,    sprintf("%s%s = %s", $ct, 'user',  $rr->addresser->user);

                    unless( Sisimai::Address->is_mailerdaemon($cv) ) {
                        # Is not a MAILER-DAEMON
                        ok length  $rr->addresser->host, sprintf("%s%s = %s", $ct, 'host',  $rr->addresser->host);
                        ok length $cv,                   sprintf("%s%s = %s", $ct, 'address', $cv);
                        ok $cv =~ $cr,                   sprintf("%s%s = %s", $ct, 'address', $cv);

                        is $cv, $rr->addresser->user.'@'.$rr->addresser->host;
                        ok $rr->addresser->alias =~ $cr if length $rr->addresser->alias;
                        ok $rr->addresser->verp  =~ $cr if length $rr->addresser->verp;
                    }
                }

                ALIAS: {
                    $cv = $rr->alias;
                    $ct = sprintf("[%s-%02d] ->alias =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                    ok $cv ne $rr->recipient->address, sprintf("%s %s != %s", $ct, $cv, $rr->recipient->address);
                }

                CATCH: {
                    is $rr->catch, undef, sprintf("[%s-%02d] ->catch = undef", $e, $errorindex);
                }

                DELIVERYSTATUS: {
                    $cv = $rr->deliverystatus;
                    $cr = qr/\A[245][.]\d[.]\d{1,3}\z/;
                    $ct = sprintf("[%s-%02d] ->deliverystatus =", $e, $errorindex);

                    if( $rr->reason eq 'feedback' || $rr->reason eq 'vacation') {
                        # "deliverystatus" is empty when the value of "reason" is "feedback"
                        is $cv, '', sprintf("%s %s", $ct, $cr);

                    } else {
                        # Except the value of "reason" is "feedback"
                        ok length $cv, sprintf("%s %s", $ct, $cv);
                        ok $cv =~ $cr, sprintf("%s %s", $ct, $cr);
                    }
                    is $cv, $cx->[$errorindex - 1]->[0], sprintf("%s %s", $ct, $cv);
                }

                DESTINATION: {
                    $cv = $rr->destination;
                    $cr = qr/\A[-_.0-9A-Za-z]+\z/;
                    $ct = sprintf("[%s-%02d] ->destination =", $e, $errorindex);

                    ok length $cv,               sprintf("%s %s", $ct, $cv);
                    ok $cv =~ $cr,               sprintf("%s %s", $ct, $cr);
                    is $cv, $rr->recipient->host, sprintf("%s recipient->host", $ct);
                }

                DIAGNOSTICCODE: {
                    $cv = $rr->diagnosticcode;
                    $ct = sprintf("[%s-%02d] ->diagnosticcode =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                }

                DIAGNOSTICTYPE: {
                    $cv = $rr->diagnostictype;
                    $cr = qr/\A(?:LMTP|SMTP|UNKNOWN|X[.]?[45]00|X-[0-9A-Z-]+)/;
                    $ct = sprintf("[%s-%02d] ->diagnostictype =", $e, $errorindex);

                    if( $rr->reason eq 'feedback' || $rr->reason eq 'vacation' ) {
                        # "deliverystatus" is empty when the value of "reason" is "feedback"
                        ok defined $cv, sprintf("%s %s", $ct, $cr);

                    } else {
                        # Except the value of "reason" is "feedback"
                        ok length $cv, sprintf("%s %s", $ct, $cv);
                        like $cv, $cr, sprintf("%s %s", $ct, $cr);
                    }
                }

                FEEDBACKID: {
                    $cv = $rr->feedbackid;
                    $ct = sprintf("[%s-%02d] ->feedbackid =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                    if( grep { index($_, $cf) > -1 } @$feedbackid ) {
                        ok length $cv, sprintf("%s %s", $ct, $cv);
                    }
                }

                FEEDBACKTYPE: {
                    $cv = $rr->feedbacktype;
                    $cr = qr/\A[\x21-\x7e]+\z/;
                    $ct = sprintf("[%s-%02d] ->feedbacktype =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);

                    if( $rr->reason eq 'feedback' ) {
                        # The value of "feedbacktype" is not empty
                        ok length $cv,                        sprintf("%s %s", $ct, $cv);
                        ok $cv =~ $cr,                        sprintf("%s %s", $ct, $cr);
                        is $cv, $cx->[$errorindex - 1]->[4],  sprintf("%s %s", $ct, $cv);

                    } else {
                        # The value of "feedbacktype" is empty
                        is $cv, '', sprintf("%s %s", $ct, $cr);
                    }
                }

                HARDBOUNCE: {
                    $cv = $rr->hardbounce;
                    $cr = qr/\A[01]\z/;
                    $ct = sprintf("[%s-%02d] ->hardbounce =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %d", $ct, $cv);
                    ok $cv =~ $cr,  sprintf("%s %s", $ct, $cr);
                    is $cv, $cx->[$errorindex - 1]->[3], sprintf("%s %d", $ct, $cv);
                }

                LHOST: {
                    $cv = $rr->lhost;
                    $cr = qr/\A[^\s\[\]\(\)]+\z/;
                    $ct = sprintf("[%s-%02d] ->lhost =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                    ok $cv =~ $cr,  sprintf("%s %s", $ct, $cr) if length $cv;
                }

                LISTID: {
                    $cv = $rr->listid;
                    $cr = qr/\A[\x21-\x7e]+\z/;
                    $ct = sprintf("[%s-%02d] ->listid =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);

                    if( length $cv ) {
                        # The value of "listid" is not empty
                        ok length $cv, sprintf("%s %s", $ct, $cv);
                        ok $cv =~ $cr, sprintf("%s %s", $ct, $cr);

                    } else {
                        # The value of "listid" is empty
                        is $cv, '', sprintf("%s %s", $ct, $cr);
                    }
                }

                MESSAGEID: {
                    $cv = $rr->messageid;
                    $cr = qr/\A[\x21-\x7e]+\z/;
                    $ct = sprintf("[%s-%02d] ->messageid =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);

                    if( length $cv ) {
                        # The value of "messageid" is not empty
                        ok length $cv, sprintf("%s %s", $ct, $cv);
                        ok $cv =~ $cr, sprintf("%s %s", $ct, $cr);

                    } else {
                        # The value of "messageid" is empty
                        is $cv, '', sprintf("%s %s", $ct, $cr);
                    }
                }

                ORIGIN: {
                    $cv = $rr->origin;
                    $ct = sprintf("[%s-%02d] ->origin =", $e, $errorindex);

                    ok length $cv, sprintf("%s %s", $ct, $cv);
                    ok -f $cv,     sprintf("%s %s: file", $ct, $cv);
                    ok -s $cv,     sprintf("%s %s: %dKB", $ct, $cv, int((-s $cv) / 1000));
                }

                REASON: {
                    $cv = $rr->reason;
                    $ct = sprintf("[%s-%02d] ->reason =", $e, $errorindex);

                    ok length $cv,                        sprintf("%s %s", $ct, $cv);
                    ok grep { $cv eq $_ } (@$reasonlist), sprintf("%s %s", $ct, $cv);
                    is $cv, $cx->[$errorindex - 1]->[2],  sprintf("%s %s", $ct, $cv);
                }

                RECIPIENT: {
                    isa_ok $rr->recipient, 'Sisimai::Address';

                    $cv = $rr->recipient->address;
                    $cr = qr/\A.+[@][0-9A-Za-z._-]+[A-Za-z]+?\z/;
                    $ct = sprintf("[%s-%02d] ->recipient->", $e, $errorindex);

                    ok defined $rr->recipient->alias,   sprintf("%s%s = %s", $ct, 'alias', $rr->recipient->alias);
                    ok defined $rr->recipient->verp,    sprintf("%s%s = %s", $ct, 'verp',  $rr->recipient->verp);
                    ok defined $rr->recipient->name,    sprintf("%s%s = %s", $ct, 'name',  $rr->recipient->name);
                    ok defined $rr->recipient->comment, sprintf("%s%s = %s", $ct, 'comment', $rr->recipient->comment);
                    ok length  $rr->recipient->user,    sprintf("%s%s = %s", $ct, 'user',  $rr->recipient->user);
                    ok length  $rr->recipient->host,    sprintf("%s%s = %s", $ct, 'host',  $rr->recipient->host);

                    ok $cv =~ $cr, sprintf("%s%s = %s", $ct, 'address', $cv);
                    ok length $cv, sprintf("%s%s = %s", $ct, 'address', $cv);
                    is $cv, sprintf("%s@%s", $rr->recipient->user, $rr->recipient->host);
                    ok $rr->recipient->alias =~ $cr if length $rr->recipient->alias;
                    ok $rr->recipient->verp  =~ $cr if length $rr->recipient->verp;
                }

                REPLYCODE: {
                    $cv = $rr->replycode;
                    $cr = qr/\A[245]\d\d\z/;
                    $ct = sprintf("[%s-%02d] ->replycode =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);

                    if ( length $cv ) {
                        # The value of "replycode" is not empty
                        ok $cv =~ $cr, sprintf("%s %s", $ct, $cv);

                        my $rv = int substr($cv, 0, 1);
                        my $dv = int substr($rr->deliverystatus, 0, 1);
                        is $rv, $dv, sprintf("%s %dXX (%d.X.X)", $ct, $rv, $dv);
                    }
                    is $cv, $cx->[$errorindex - 1]->[1], sprintf("%s %s", $ct, $cv);
                }

                RHOST: {
                    $cv = $rr->rhost;
                    $cr = qr/\A[^\s\[\]\(\)]+\z/;
                    $ct = sprintf("[%s-%02d] ->rhost =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                    ok $cv =~ $cr,  sprintf("%s %s", $ct, $cr) if length $cv;
                }

                SENDERDOMAIN: {
                    $cv = $rr->senderdomain;
                    $cr = qr/\A[-_.0-9A-Za-z]+\z/;
                    $ct = sprintf("[%s-%02d] ->senderdomain =", $e, $errorindex);

                    unless( Sisimai::Address->is_mailerdaemon($rr->addresser->address)) {
                        ok length $cv,               sprintf("%s %s", $ct, $cv);
                        ok $cv =~ $cr,               sprintf("%s %s", $ct, $cr);
                        is $cv, $rr->addresser->host, sprintf("%s addresser->host", $ct);
                    }
                }

                DECODEDBY: {
                    $cv = $rr->decodedby;
                    $cr = qr/\A[-.0-9A-Za-z]+\z/;
                    $ct = sprintf("[%s-%02d] ->decodedby =", $e, $errorindex);

                    ok length $cv, sprintf("%s %s", $ct, $cv);
                    if( index($nameprefix, 'rhost') == 0 ) {
                        # Sisimai::Rhost
                        ok $cv =~ $cr, sprintf("%s %s", $ct, $cr);

                    } else {
                        # Sisimai::Lhost
                        if( $E eq 'RFC3464' && $cv !~ /\ARFC3464/ ) {
                            # Parsed by Sisimai::LDA
                            ok $cv =~ $cr, sprintf("%s %s", $ct, $cr);
                            is scalar @{ [grep { $cv eq $_ } @$lhostindex] }, 0, sprintf("%s %s", $ct, $cv);

                        } elsif( grep { $E eq $_ } $alternates->{'RFC3464'}->@* ) {
                            # Decoded by Sisimai::RFC3464 since v5.2.0
                            is $cv, 'RFC3464', sprintf("%s %s", $ct, $cr);

                        } elsif( grep { $E eq $_ } $alternates->{'Exchange2007'}->@* ) {
                            # Decoded by Sisimai::Lhost::Exchange2007 since v5.2.0
                            is $cv, 'Exchange2007', sprintf("%s %s", $ct, $cr);

                        } elsif( grep { $E eq $_ } $alternates->{'Exim'}->@* ) {
                            # Decoded by Sisimai::Lhost::Exim since v5.2.0
                            is $cv, 'Exim', sprintf("%s %s", $ct, $cr);

                        } elsif( grep { $E eq $_ } $alternates->{'qmail'}->@* ) {
                            # Decoded by Sisimai::Lhost::qmail since v5.2.0
                            is $cv, 'qmail', sprintf("%s %s", $ct, $cr);

                        } else {
                            # Other MTA modules
                            if( $E eq "AmazonSES" ) {
                                # lhost-amazonses-* are decoded by Sisimai::RFC3464 except 11-14
                                like $cv, qr/(?:AmazonSES|RFC3464)/, sprintf("%s %s", $ct, $cr);

                            } else {
                                is $cv, $E, sprintf("%s %s", $ct, $cr);
                            }
                        }
                    }
                }

                COMMAND: {
                    $cv = $rr->command;
                    $cr = qr/\A(?:CONN|HELO|EHLO|MAIL|RCPT|DATA|QUIT)\z/;
                    $ct = sprintf("[%s-%02d] ->command =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                    ok $cv =~ $cr,  sprintf("%s %s", $ct, $cr) if length $cv;
                }

                SUBJECT: {
                    $cv = $rr->subject;
                    $ct = sprintf("[%s-%02d] ->subject =", $e, $errorindex);

                    ok defined $cv, sprintf("%s %s", $ct, $cv);
                }

                TIMESTAMP: {
                    $cv = $rr->timestamp;
                    $ct = sprintf("[%s-%02d] ->timestamp =", $e, $errorindex);

                    isa_ok $cv, 'Sisimai::Time';
                    ok $cv->epoch, sprintf("%s %s", $ct, $cv->epoch);
                    ok $cv->cdate, sprintf("%s %s", $ct, $cv->cdate);
                }

                TIMEZONEOFFSET: {
                    $cv = $rr->timezoneoffset;
                    $cr = qr/\A[-+]\d{4}/;
                    $ct = sprintf("[%s-%02d] ->timezoneoffset =", $e, $errorindex);

                    ok length $cv, sprintf("%s %s", $ct, $cv);
                    ok $cv =~ $cr, sprintf("%s %s", $ct, $cr);
                }

                TOKEN: {
                    $cv = $rr->token;
                    $cr = qr/\A[0-9a-f]{40}\z/;
                    $ct = sprintf("[%s-%02d] ->token =", $e, $errorindex);

                    is length($cv), 40, sprintf("%s %s", $ct, $cv);
                    ok $cv =~ $cr,      sprintf("%s %s", $ct, $cr);
                }

                DAMN: {
                    $cv = $rr->damn;
                    $ct = sprintf("[%s-%02d] ->damn =", $e, $errorindex);

                    isa_ok $cv, 'HASH';
                    is $cv->{'addresser'}, $rr->addresser->address, sprintf("%s %s", $ct, $cv->{'addresser'});
                    is $cv->{'recipient'}, $rr->recipient->address, sprintf("%s %s", $ct, $cv->{'recipient'});
                    is $cv->{'timestamp'}, $rr->timestamp->epoch,   sprintf("%s %d", $ct, $cv->{'timestamp'});
                    is $cv->{'catch'},     '',                      sprintf("%s ''", $ct);
                }

                DUMP: {
                    # JSON
                    $cv = $rr->dump('json');
                    $ct = sprintf("[%s-%02d] ->dump(json) =", $e, $errorindex);
                    $cj = $jsonobject->decode($cv);

                    isa_ok $cj, 'HASH';
                    ok length $cv,         sprintf("%s %s", $ct, substr($cv, 0, 32));
                    is $cj->{'catch'}, '', sprintf("%s ''", $ct);
                    is $rr->addresser->address, $cj->{'addresser'}, sprintf("%s %s", $ct, $cj->{'addresser'});
                    is $rr->recipient->address, $cj->{'recipient'}, sprintf("%s %s", $ct, $cj->{'recipient'});
                    is $rr->timestamp->epoch,   $cj->{'timestamp'}, sprintf("%s %s", $ct, $cj->{'timestamp'});

                    eval {
                        # YAML; this module is an optional
                        require YAML;
                        $cv = $rr->dump('yaml');
                        $ct = sprintf("[%s-%02d] ->dump(yaml) =", $e, $errorindex);
                        $cj = YAML::Load($cv);

                        isa_ok $cj, 'HASH';
                        ok length $cv,         sprintf("%s %s", $ct, substr($cv, 0, 3));
                        is $cj->{'catch'}, '', sprintf("%s ''", $ct);
                        is $rr->addresser->address, $cj->{'addresser'}, sprintf("%s %s", $ct, $cj->{'addresser'});
                        is $rr->recipient->address, $cj->{'recipient'}, sprintf("%s %s", $ct, $cj->{'recipient'});
                        is $rr->timestamp->epoch,   $cj->{'timestamp'}, sprintf("%s %s", $ct, $cj->{'timestamp'});
                    };
                }

            } # End of the loop for checking each Sisimai::Fact object
            $emailindex++;
            ok $errorindex, sprintf("%s is including %d bounces", $mailobject->data->path, $errorindex);

        } # End of READ_EACH_EMAIL

    } # End of PARSE_EACH_EMAIL
    ok $emailindex, sprintf("%s have parsed %d emails", $M, $emailindex);
};

sub makeinquiry { return $moduletest }

1;
