# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::OP::Byte {
  
  use R::NDArray::Byte;
  use ByteList;
  
  # Class Methods
  static method c : R::NDArray::Byte ($data : object of Byte|byte[]|R::NDArray::Byte, $dim : int[] = undef) {
    
    if ($data) {
      if ($data is_type Byte) {
        $data = [(byte)$data->(Byte)];
      }
      elsif ($data is_type byte[]) {
        # Do nothing
      }
      elsif ($data is_type R::NDArray::Byte) {
        unless ($dim) {
          $dim = $data->(R::NDArray::Byte)->dim;
        }
        
        $data = $data->(R::NDArray::Byte)->data;
      }
      else {
        die "The type of the data \$data must be Byte, byte[], or R::NDArray::Byte if defined.";
      }
    }
    
    my $ndarray = R::NDArray::Byte->new({dim => $dim, data => $data});
    
    return $ndarray;
  }
  
  static method rep : R::NDArray::Byte ($x_ndarray : R::NDArray::Byte, $times : int) {
    return (R::NDArray::Byte)R::OP->rep($x_ndarray, $times);
  }
  
  static method rep_length : R::NDArray::Byte ($x_ndarray : R::NDArray::Byte, $length : int) {
    return (R::NDArray::Byte)R::OP->rep_length($x_ndarray, $length);
  }
  
  static method seq : R::NDArray::Byte ($begin : byte, $end : byte, $by : byte = 1) {
    
    if ($by == 0) {
      die "\$by must not be 0.";
    }
    elsif ($by > 0) {
      unless ($end >= $begin) {
        die "\$end must be greater than or equal to \$begin if \$by is greater than 0.";
      }
    }
    else {
      unless ($end <= $begin) {
        die "\$end must be less than or equal to \$begin if \$by is less than 0.";
      }
    }
    
    my $ret_data_list = ByteList->new;
    my $current = $begin;
    while (1) {
      if ($by >= 0) {
        if ($current > $end) {
          last;
        }
      }
      else {
        if ($current < $end) {
          last;
        }
      }
      $ret_data_list->push($current);
      $current += $by;
    }
    
    my $ret_data = $ret_data_list->to_array;
    
    my $ret_ndarray = R::OP::Byte->c($ret_data);
    
    return $ret_ndarray;
  }
}
