#!/usr/bin/env perl

=head1 NAME

clonehelper - Create a bunch of cloned VMs in via libvirt.

=head1 SYNOPSIS

clonehelper [B<-f> <config>] B<-b> <action>

=head1 DESCRIPTION

The basic work flow for this is like below.

   delete
   clone
   start
   wait a bit till they are all started
   snapshot
   shutdown

=head1 ACTIONS

=head2 list

Print a JSON dump of VMs, maps, and IPs.

=head2 start

Start all the VM clones.

=head2 stop

Stop all the VM clones.

=head2 clone

Generate the VM clones.

=head2 delete

Delete all the VM clones.

=head2 net_xml

Generate the XML config and print it.

=head2 net_redefine

Remove and re-add the network using the generated config.

=head2 snapshot

Snapshot all the VM clones.

=head1 CONFIG

The config format is a INI file.

The variable/value defaults are shown below.

    net=default
    # Name of the libvirt network in question.

    blank_domains=/usr/local/etc/clonehelper/blank_domains
    # List of domains to blank via setting 'dnsmasq:option value='address=/foo.bar/'.
    # If not this file does not exist, it will be skipped.

    net_head=/usr/local/etc/clonehelper/net_head
    # The top part of the net XML config that that dnsmasq options will be
    # sandwhiched between.

    net_tail=/usr/local/etc/clonehelper/net_tail
    # The bottom part of the net XML config that that dnsmasq options will
    # be sandwhiched between.

    windows_blank=1
    # Blank commonly used MS domains. This is handy for reducing network noise
    # when testing as well as making sure they any VMs don't do something like
    # run updates when one does not want it to.

    mac_base=00:08:74:2d:dd:
    # Base to use for the MAC.

    ipv4_base=192.168.1.
    # Base to use for the IPs for adding static assignments.

    start=100
    # Where to start in set.

    to_clone=baseVM
    # The name of the VM to clone.

    clone_name_base=cloneVM
    # Base name to use for creating the clones. 'foo' will become 'foo$current', so
    # for a start of 100, the first one would be 'foo100' and with a count of 10 the
    # last will be 'foo109'.

    count=10
    # How many clones to create.

    snapshot_name=clean
    # The name to use for the snapshot.

temp

=cut

use strict;
use warnings;
use Getopt::Long;
use Config::Tiny;
use JSON;
use VM::Libvirt::CloneHelper;

sub version {
	print "clonehelper v. 0.0.1\n";
}

sub help {
	&version;

	print '

-f <file>        Config file.
-a <action>      Action to perform.

-h               Print help.
--help           Print help.
-v               Print version.
--version        Print version.


Actions...
list           Print a JSON dump of VMs, maps, and IPs.
start          Start all the VM clones.
stop           Stop all the VM clones.
clone          Generate the VM clones.
delete         Delete all the VM clones.
net_xml        Generate the XML config and print it.
net_redefine   Remove and re-add the network using the generated config.
snapshot       Snapshot all the VM clones.
';

}

my $config_file = '/usr/local/etc/clonehelper/config.ini';
my $action      = 'list';
my $help        = 0;
my $version     = 0;
GetOptions(
	'version' => \$version,
	'v'       => \$version,
	'help'    => \$help,
	'h'       => \$help,
	'f=s'     => \$config_file,
	'a=s'     => \$action,
);

# print version or help if requested
if ($help) {
	&help;
	exit 42;
}
if ($version) {
	&version;
	exit 42;
}

# die if the config file does not exist
if ( !-f $config_file ) {
	die $config_file . ' does not exist';
}

# read the config
my $config = Config::Tiny->read( $config_file, 'utf8' )
	or die( 'unable to read config("' . $config_file . '")... ' . $@ );

my $clone_helper = VM::Libvirt::CloneHelper->new($config->{_});

if ( $action eq 'list' ) {
	my $json = JSON->new->allow_nonref->pretty(1)->canonical(1);
	print $json->encode( $clone_helper->vm_list );
}
elsif ( $action eq 'start' ) {
	$clone_helper->start_clones;
}
elsif ( $action eq 'stop' ) {
	$clone_helper->stop_clones;
}
elsif ( $action eq 'delete' ) {
	$clone_helper->delete_clones;
}
elsif ( $action eq 'snapshot' ) {
	$clone_helper->snapshot_clones;
}
elsif ( $action eq 'net_xml' ) {
	my $xml = $clone_helper->net_xml;
	print $xml;
}
elsif ( $action eq 'net_redefine' ) {
	$clone_helper->net_redefine;
}
elsif ( $action eq 'clone' ) {
	$clone_helper->clone;
}
else {
	die $action . ' is not a understood action';
}

exit 0;
