# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Time::Piece extends R::NDArray {
  
  use Fn;
  use Array;
  use Time::Piece;
  
  # Fields
  method data : Time::Piece[] () {
    
    return (Time::Piece[])$self->{data};
  }
  
  # Class Mehtods
  static method new : R::NDArray::Time::Piece ($options : object[] = undef) {
    
    my $self = new R::NDArray::Time::Piece;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method create_default_data : Time::Piece[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new Time::Piece[$length];
    
    return $data;
  }
  
  method elem_to_string : string ($data : Time::Piece[], $data_index : int) {
    
    my $string = (string)undef;
    if ($data->[$data_index]) {
      $string = $data->[$data_index]->strftime("%Y-%m-%d %H:%M:%S");
    }
    
    return $string;
  }
  
  method elem_assign : void ($dist_data : Time::Piece[], $dist_data_index : int, $src_data : Time::Piece[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_clone : void ($dist_data : Time::Piece[], $dist_data_index : int, $src_data : Time::Piece[], $src_data_index : int) {
    
    if ($src_data->[$src_data_index]) {
      $dist_data->[$dist_data_index] = $src_data->[$src_data_index]->clone;
    }
  }
  
  method elem_cmp : int ($a_data : Time::Piece[], $a_data_index : int, $b_data : Time::Piece[], $b_data_index : int) {
    
    my $cmp = 0;
    if ($a_data->[$a_data_index] && $b_data->[$b_data_index]) {
      my $a_epoch = $a_data->[$a_data_index]->epoch;
      
      my $b_epoch = $b_data->[$b_data_index]->epoch;
      
      $cmp = $a_epoch <=> $b_epoch;
    }
    elsif ($a_data->[$a_data_index]) {
      $cmp = 1;
    }
    elsif ($b_data->[$b_data_index]) {
      $cmp = -1;
    }
    
    return $cmp;
  }
  
  method elem_is_na : int ($data : Time::Piece[], $data_index : int) {
    
    my $elem_is_na = 0;
    if (!$data->[$data_index]) {
      $elem_is_na = 1;
    }
    
    return $elem_is_na;
  }
  
  method clone : R::NDArray::Time::Piece ($shallow : int = 0) {
    return (R::NDArray::Time::Piece)$self->SUPER::clone($shallow);
  }
  
  method slice : R::NDArray::Time::Piece ($indexes_product : R::NDArray::Int[]) {
    return (R::NDArray::Time::Piece)$self->SUPER::slice($indexes_product);
  }
  
}
