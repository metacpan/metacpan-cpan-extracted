# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Float extends R::NDArray {
  
  # Class Mehtods
  static method new : R::NDArray::Float ($options : object[] = undef) {
    
    my $self = new R::NDArray::Float;
    
    $self->init($options);
    
    return $self;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    $options = Fn->merge_options($options, {element_size => 4, type => "float"});
    
    $self->SUPER::init($options);
  }
  
  method data : float[] () {
    
    return (float[])$self->{data};
  }
  
  protected method check : void () {
    
    my $x_data = (float[])$self->{data};
    
    my $length = $self->length;
    
    unless (@$x_data == $length) {
      die "The length of data field must be the same as length field.";
    }
  }
  
  protected method create_default_data : float[] ($length : int) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $x_data = new float[$length];
    
    return $x_data;
  }
  
  method clone : R::NDArray::Float () {
    return (R::NDArray::Float)$self->SUPER::clone;
  }
  
  method element_to_string : string ($data_index : int) {
    
    my $data = $self->data;
    
    my $element = $data->[$data_index];
    
    my $string = (string)(float)$element;
    
    return $string;
  }
  
  precompile method set_order : void ($indexes : int[]) {
    
    $self->check_set_order_args($indexes);
    
    my $data = (float[])$self->{data};
    
    my $tmp = $self->clone;
    
    my $tmp_data = $tmp->data;
    
    my $length = $self->length;
    for (my $i = 0; $i < $length; $i++) {
      my $order_index = $indexes->[$i];
      
      $data->[$i] = $tmp_data->[$order_index];
    }
  }
}
