# Copyright (c) 2024 Yuki Kimoto
# MIT License

# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Byte extends R::NDArray {
  
  use Sort;
  
  # Class Mehtods
  static method new : R::NDArray::Byte ($options : object[] = undef) {
    
    my $self = new R::NDArray::Byte;
    
    $self->init($options);
    
    return $self;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    $self->SUPER::init($options);
  }
  
  method data : byte[] () {
    
    return (byte[])$self->{data};
  }
  
  protected method check : void () {
    
    my $data = (byte[])$self->{data};
    
    my $length = $self->length;
    
    unless (@$data == $length) {
      die "The length of data field must be the same as length field.";
    }
  }
  
  protected method create_default_data : byte[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new byte[$length];
    
    return $data;
  }
  
  method clone : R::NDArray::Byte ($elem_shallow_clone : int = 0) {
    return (R::NDArray::Byte)$self->SUPER::clone($elem_shallow_clone);
  }
  
  method elem_to_string : string ($data_index : int) {
    
    my $data = $self->data;
    
    my $elem = $data->[$data_index];
    
    my $string = (string)(byte)$elem;
    
    return $string;
  }
  
  method elem_to_object : Byte ($data_index : int) {
    
    my $data = $self->data;
    
    my $elem = $data->[$data_index];
    
    my $object = (Byte)$elem;
    
    return $object;
  }
  
  precompile method set_order : void ($indexes_ndarray : R::NDArray::Int) {
    
    $self->check_set_order_args($indexes_ndarray);
    
    my $indexes = $indexes_ndarray->data;
    
    my $data = (byte[])$self->{data};
    
    my $tmp = $self->clone;
    
    my $tmp_data = $tmp->data;
    
    my $length = $self->length;
    for (my $i = 0; $i < $length; $i++) {
      my $order_index = $indexes->[$i];
      
      $data->[$i] = $tmp_data->[$order_index];
    }
  }
  
  protected method slice_data : byte[] ($data_indexes : int[]) {
    
    unless ($data_indexes) {
      die "The data indexes \$data_indexes must be defined.";
    }
    
    my $data_indexes_length = @$data_indexes;
    my $data = $self->data;
    my $ret_data = new byte[$data_indexes_length];
    for (my $i = 0; $i < $data_indexes_length; $i++) {
      my $data_index = $data_indexes->[$i];
      $ret_data->[$i] = $data->[$data_index];
    }
    
    return $ret_data;
  }
  
  protected precompile method slice_set_data : void ($data_indexes : int[], $data : byte[]) {
    
    my $self_data = $self->data;
    
    unless ($data_indexes) {
      die "The data indexes \$data_indexes must be defined.";
    }
    
    for (my $i = 0; $i < @$data; $i++) {
      my $data_index = $data_indexes->[$i];
      $self_data->[$data_index] = $data->[$i];
    }
  }
  
  method slice : R::NDArray::Byte ($vector_indexes : R::NDArray::Int[]) {
    return (R::NDArray::Byte)$self->SUPER::slice($vector_indexes);
  }
  
  method slice_set : void ($vector_indexes : R::NDArray::Int[], $ndarray : R::NDArray::Byte) {
    $self->SUPER::slice_set($vector_indexes, $ndarray);
  }
  
  method elem_cmp_old : int ($a_elem : Byte, $b_elem : Byte) {
    
    my $cmp = (byte)$a_elem <=> (byte)$b_elem;
    
    return $cmp;
  }
  
  method elem_cmp : int ($a_data_index : int, $b_data_index : int) {
    
    my $data = $self->data;
    
    my $cmp = $data->[$a_data_index] <=> $data->[$b_data_index];
    
    return $cmp;
  }
  
  method sort_asc : void () {
    
    unless ($self->is_vector) {
      die "The instance must be a vector.";
    }
    
    my $data = $self->data;
    
    Sort->sort_byte_asc($data);
  }
  
  method sort_desc : void () {
    
    unless ($self->is_vector) {
      die "The instance must be a vector.";
    }
    
    my $data = $self->data;
    
    Sort->sort_byte_desc($data);
  }

}
