package Alien::MSYS;

use strict;
use warnings;
use base qw( Exporter );
use File::ShareDir qw( dist_dir );
use File::Spec;

our @EXPORT    = qw( msys msys_run );
our @EXPORT_OK = qw( msys msys_run msys_path );

# ABSTRACT: Tools required for GNU style configure scripts on Windows
our $VERSION = '0.07'; # VERSION


sub msys (&)
{
  local $ENV{PATH} = $^O eq 'MSWin32' ? msys_path().";$ENV{PATH}" : $ENV{PATH};
  $_[0]->();
}


sub msys_run
{
  my $cmd = \@_;
  $cmd = \@ARGV unless @$cmd > 0;
  msys { system @$cmd };
  # child did an exit(0), or "success"
  return if $? == 0;
  # child wasn't able to exit (-1) or died with signal
  exit 2 if $? == -1 || $? & 127;
  # child exited with non zero
  exit $?;
}


sub msys_path ()
{
  return undef unless  $^O eq 'MSWin32';
  return $ENV{PERL_ALIEN_MSYS_BIN} if defined $ENV{PERL_ALIEN_MSYS_BIN};
  
  require File::Spec;
  foreach my $dir (split /;/, $ENV{PATH})
  {
    my $path = eval {
      my $mingw_get = File::Spec->catfile($dir, 'mingw-get.exe');
      die 'no mingw-get.exe' unless -x $mingw_get;
      my($volume, $dirs) = File::Spec->splitpath($mingw_get);
      my @dirs = File::Spec->splitdir($dirs);
      splice @dirs, -2;
      push @dirs, qw( msys 1.0 bin );
      my $path = File::Spec->catdir($volume, @dirs);
      die 'no sh.exe' unless -x File::Spec->catfile($path, 'sh.exe');
      $path;
    };
    return $path unless $@;
  }

  foreach my $dir (qw( C:\MinGW\msys\1.0\bin ))
  {
    return $dir if -x File::Spec->catfile($dir, 'sh.exe');
  }

  my $path = eval {
    require File::HomeDir;
    require Win32::Shortcut;
    my $lnk_name = File::Spec->catfile(File::HomeDir->my_desktop, 'MinGW Installer.lnk');
    die "No MinGW Installer.lnk" unless -r $lnk_name;
    my $lnk      = Win32::Shortcut->new;
    $lnk->Load($lnk_name);
    my($volume, $dirs) = File::Spec->splitpath($lnk->{Path});
    my @dirs = File::Spec->splitdir($dirs);
    splice @dirs, -3;
    push @dirs, qw( msys 1.0 bin );
    my $path = File::Spec->catdir($volume, @dirs);
    die 'no sh.exe' unless -x File::Spec->catfile($path, 'sh.exe');
    $path;
  };

  return $path unless $@;

  my $dir = _my_dist_dir();
  return $dir if defined $dir && -d $dir;

  return undef;
}

sub _my_dist_dir
{
  eval { File::Spec->catdir(dist_dir('Alien-MSYS'), qw( msys 1.0 bin )) };
}

1;

__END__

=pod

=encoding UTF-8

=head1 NAME

Alien::MSYS - Tools required for GNU style configure scripts on Windows

=head1 VERSION

version 0.07

=head1 SYNOPSIS

from Perl:

 use Alien::MSYS;
 # runs uname from MSYS
 my $uname = mysy { `uname` };

From Prompt/Makefile

 C:\> perl -MAlien::MSYS -e msys_run uname

=head1 DESCRIPTION

MSYS provides minimal shell and POSIX tools on Windows to enable GNU style configure 
scripts to run (the type usually generated by C<autoconf>). This module aims to 
provide an interface for using MSYS on Windows and act as a no-op on Unix like 
operating systems which already have that capability.  If you use this module, I 
recommend that you list this as a prerequisite only during MSWin32 installs.

When installing, this distribution will look for an existing C<MSYS> using the following
methods in this order:

=over 4

=item environment variable C<PERL_ALIEN_MSYS_BIN>

If set, this environment variable should be set to the root of C<MSYS> (NOT C<MinGW>).
For example, if you have C<MinGW> / C<MSYS> installed on C<D:> you might use this:

 C:\> set PERL_ALIEN_MSYS_BIN=D:\MinGW\msys\1.0\bin

Keep in mind that this environment variable is consulted during both install and at run-time,
so it is advisable to set this in the System Properties control panel.

=item search C<PATH> for C<mingw-get.exe>

First L<Alien::MSYS> searches the C<PATH> environment variable for the C<mingw-get.exe>
program, which is a common method for installing C<MinGW> and C<MSYS>.  From there
if it can deduce the location of C<MSYS> it will use that.

=item try C<C:\MinGW\msys\1.0\bin>

This is usually the default location, so L<Alien::MSYS> will try this directory
even if it isn't found by another method.

=item Use desktop shortcut for C<MinGW Installer>

Usually when you install the C<MinGW> installer it creates a shortcut on the desktop.
if L<Win32::Shortcut> is installed (it is an optional dependency), then L<Alien::MSYS>
can use that information to determine the location of C<MSYS>.

=back

If C<MSYS> cannot be found using any of these methods, then it will download and install
C<MSYS> in this distribution's share directory (via L<File::ShareDir>).

=head1 FUNCTIONS

=head2 msys

 # get the uname from MSYS
 my $uname = msys { `uanem` };
 
 # run with GNU make from MSYS instead of
 # dmake from Strawberry Perl
 msys { system 'make' };

This function takes a single argument, a code reference, and runs it with the correctly
set environment so that calls to the system function or the qx quote like operator will
use MSYS instead of the default environment.

=head2 msys_run

 # pass command through @ARGV
 C:\> perl -MAlien::MSYS -e msys_run uname
 
 # pass command through @_
 C:\> perl -MAlien::MSYS -e "msys_run 'make'; msys_run 'make install'"

This function runs a command with the MSYS environment.  It gets the command and arguments
either as passed to it, or if none are passed the the command is expected to be in
@ARGV.

If the command fails then it will L<exit|perlfunc#exit> with a non-zero error code.  This
is useful, in the second example above if either C<make> or C<make install> fails, then
the whole command will fail, also C<make install> will not be attempted unless C<make>
succeeds.

=head2 msys_path

This function returns the full path to the MSYS bin directory.

=head1 AUTHOR

Graham Ollis <plicease@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2013 by Graham Ollis.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
