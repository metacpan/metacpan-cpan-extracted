# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Short extends R::NDArray {
  
  # Class Mehtods
  static method new : R::NDArray::Short ($options : object[] = undef) {
    
    my $self = new R::NDArray::Short;
    
    $self->init($options);
    
    return $self;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    $self->SUPER::init($options);
  }
  
  method data : short[] () {
    
    return (short[])$self->{data};
  }
  
  protected method check : void () {
    
    my $data = (short[])$self->{data};
    
    my $length = $self->length;
    
    unless (@$data == $length) {
      die "The length of data field must be the same as length field.";
    }
  }
  
  protected method create_default_data : short[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new short[$length];
    
    return $data;
  }
  
  method clone : R::NDArray::Short () {
    return (R::NDArray::Short)$self->SUPER::clone;
  }
  
  method elem_to_string : string ($data_index : int) {
    my $data = $self->data;
    
    my $elem = $data->[$data_index];
    
    my $string = (string)(short)$elem;
    
    return $string;
  }
  
  precompile method set_order : void ($indexes_ndarray : R::NDArray::Int) {
    
    $self->check_set_order_args($indexes_ndarray);
    
    my $indexes = $indexes_ndarray->data;
    
    my $data = (short[])$self->{data};
    
    my $tmp = $self->clone;
    
    my $tmp_data = $tmp->data;
    
    my $length = $self->length;
    for (my $i = 0; $i < $length; $i++) {
      my $order_index = $indexes->[$i];
      
      $data->[$i] = $tmp_data->[$order_index];
    }
  }
  
  protected method slice_data : object ($data_indexes : int[]) {
    
    unless ($data_indexes) {
      die "The data indexes \$data_indexes must be defined.";
    }
    
    my $data_indexes_length = @$data_indexes;
    my $data = $self->data;
    my $ret_data = new short[$data_indexes_length];
    for (my $i = 0; $i < $data_indexes_length; $i++) {
      $ret_data->(short[])->[$i] = $data->(short[])->[$i];
    }
    
    return $ret_data;
  }
  
  protected method slice_set_data : void ($data_indexes : int[], $data : object) {
    
    my $self_data = $self->{data};
    
    unless ($data_indexes) {
      die "The data indexes \$data_indexes must be defined.";
    }
    
    my $data_index = 0;
    my $data_indexes_length = @$data_indexes;
    for (my $i = 0; $i < $data_indexes_length; $i++) {
      $self_data->(short[])->[$i] = $data->(short[])->[$data_index];
      $data_index++;
    }
  }
  
}
