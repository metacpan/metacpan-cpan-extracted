use alienfile;

plugin 'PkgConfig' => 'yaml-0.1';

share {

  plugin Download => (
    url     => 'http://pyyaml.org/download/libyaml/',
    version => qr/^yaml-([0-9\.]+)\.tar\.gz$/,
  );
  plugin Extract => 'tar.gz';

  requires 'Path::Tiny' => '0.077';

  patch sub {
    my($build) = @_;

    my $pc = Path::Tiny->new('yaml-0.1.pc.in');
    if(-f $pc)
    {
      $pc->edit_lines(sub {
        s/^Cflags:\s*$/Cflags: -I\${includedir}\n/;
      });
    }
  };

  plugin 'Build::Autoconf' => ();

  ffi {

    build [
      '%{configure} --enable-shared --disable-static --libdir=%{.install.autoconf_prefix}/dynamic',
      '%{make}',
      '%{make} install',
    ];

  };

  meta->after_hook(
    gather_share => sub {
      my($build) = @_;
      $build->runtime_prop->{$_} .= ' -DYAML_DECLARE_STATIC ' for qw( cflags cflags_static );
    },
  );

};
